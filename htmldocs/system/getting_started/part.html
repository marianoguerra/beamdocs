<!doctype html>
<html><head><meta charset="utf-8"><link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.0/css/bootstrap.min.css"></head><body style="margin: 4em 10%"><h1>Getting Started With Erlang</h1><a name="getting started"></a><p>This section is a quick start tutorial to get you started with Erlang.
Everything in this section is true, but only part of the truth. For example,
only the simplest form of the syntax is shown, not all esoteric forms.
Also, parts that are greatly simplified are indicated with *manual*.
This means that a lot more information on the subject is to be found in
the Erlang book or in
<a href="../doc/reference_manual/introduction#erlang ref manual"> Erlang Reference Manual</a>.</p><h3>Prerequisites</h3><p>The reader of this section is assumed to be familiar with the following:</p><ul><li>Computers in general</li><li>Basics on how computers are programmed</li></ul><h3>Omitted Topics</h3><p>The following topics are not treated in this section:</p><ul><li>References.</li><li>Local error handling (catch/throw).</li><li>Single direction links (monitor).</li><li>Handling of binary data (binaries / bit syntax).</li><li>List comprehensions.</li><li>How to communicate with the outside world and software written in other languages (ports); this is described in <a href="../doc/tutorial/introduction#interoperability tutorial"> Interoperability Tutorial</a>.</li><li>Erlang libraries (for example, file handling).</li><li>OTP and (in consequence) the Mnesia database.</li><li>Hash tables for Erlang terms (ETS).</li><li>Changing code in running systems.</li></ul><h3>The Erlang Shell</h3><p>
Most operating systems have a command interpreter or shell, UNIX
and Linux have many, Windows has the command prompt. Erlang has
its own shell where bits of Erlang code can be written directly,
and be evaluated to see what happens
(see the <a href="./shell">shell(3)</a>
manual page in STDLIB).
</p><p>Start
the Erlang shell (in Linux or UNIX) by starting a shell or
command interpreter in your operating system and typing
<strong>erl</strong>. You will see something like this.</p><pre>
% <span class="input">erl</span>
Erlang R15B (erts-5.9.1) [source] [smp:8:8] [rq:8] [async-threads:0] [hipe] [kernel-poll:false]

Eshell V5.9.1  (abort with ^G)
1&gt;</pre><p>Type "2 + 5." in the shell and then press Enter (carriage return).
Notice that you tell the shell you are done entering code by finishing
with a full stop "." and a carriage return.</p><pre>
1&gt; <span class="input">2 + 5.</span>
7
2&gt;</pre><p>As shown, the Erlang shell numbers the lines that
can be entered, (as 1&gt; 2&gt;) and that it correctly says
that 2 + 5 is 7. If you make writing mistakes in the shell,
you can delete with the backspace key, as in most shells.
There are many more editing commands in the shell
(see <a href="./tty">tty - A command line interface</a> in ERTS User's Guide).</p><p>(Notice that many line numbers given by the shell in the
following examples are out of sequence. This is because this
tutorial was written and code-tested in separate sessions).</p><p>Here is a bit more complex calculation:</p><pre>
2&gt; <span class="input">(42 + 77) * 66 / 3.</span>
2618.0</pre><p>Notice the use of brackets, the multiplication operator "*",
and the division operator "/", as in normal arithmetic (see
<a href="./expressions">Expressions</a>).</p><p>Press Control-C to shut down the Erlang system and the Erlang
shell.</p><p>The following output is shown:</p><pre>
BREAK: (a)bort (c)ontinue (p)roc info (i)nfo (l)oaded
       (v)ersion (k)ill (D)b-tables (d)istribution
<span class="input">a</span>
%</pre><p>Type "a" to leave the Erlang system.</p><p>Another way to shut down the Erlang system is by entering
<strong>halt()</strong>:</p><pre>
3&gt; <span class="input">halt().</span>
% </pre><h3>Modules and Functions</h3><p>A programming language is not much use if you only can run code
from the shell. So here is a small Erlang program. Enter it into
a file named <strong>tut.erl</strong> using a suitable
text editor. The file name <strong>tut.erl</strong> is important, and also
that it is in the same directory as the one where you started
<strong>erl</strong>). If you are lucky your editor has an Erlang mode
that makes it easier for you to enter and format your code
nicely (see <a href="./erlang_mode_chapter">The Erlang mode for Emacs</a> in Tools User's Guide), but you can manage
perfectly well without. Here is the code to enter:</p><pre><code class="">
-module(tut).
-export([double/1]).

double(X) -&gt;
    2 * X.</code></pre><p>It is not hard to guess that this program doubles the value of
numbers. The first two lines of the code are described later.
Let us compile the program. This can be done in an Erlang shell
as follows, where <strong>c</strong> means compile:</p><pre>
3&gt; <span class="input">c(tut).</span>
{ok,tut}</pre><p>The <strong>{ok,tut}</strong> means that the compilation is OK. If it
says "error" it means that there is some mistake in the text
that you entered. Additional error messages gives an idea to
what is wrong so you can modify the text and then try to compile
the program again.</p><p>Now run the program:</p><pre>
4&gt; <span class="input">tut:double(10).</span>
20</pre><p>As expected, double of 10 is 20.</p><p>Now let us get back to the first two lines of the code. Erlang
programs are
written in files. Each file contains an Erlang
<em>module</em>. The first line of code in the module is
the module name (see
<a href="./modules">Modules</a>):</p><pre><code class="">
-module(tut).</code></pre><p>Thus, the module is called <em>tut</em>. Notice
the full stop "." at the end of the line. The files which are
used to store
the module must have the same name as the module but with
the extension ".erl". In this case the file name is <strong>tut.erl</strong>.
When using a function in another module, the syntax
<strong>module_name:function_name(arguments)</strong> is used. So the
following means call function <strong>double</strong> in module <strong>tut</strong>
with argument "10".</p><pre>
4&gt; <span class="input">tut:double(10).</span></pre><p>The second line says that the module <strong>tut</strong> contains a
function called <strong>double</strong>, which takes one argument
(<strong>X</strong> in our example):</p><pre><code class="">
-export([double/1]).</code></pre><p>The second line also says that this function can be called from
outside the module <strong>tut</strong>. More about this later. Again,
notice the "." at the end of the line.</p><p>Now for a more complicated example, the factorial of a number.
For example, the factorial of 4 is 4 * 3 * 2 * 1, which equals 24.</p><p>Enter the following code in a file named <strong>tut1.erl</strong>:</p><pre><code class="">
-module(tut1).
-export([fac/1]).

fac(1) -&gt;
    1;
fac(N) -&gt;
    N * fac(N - 1).</code></pre><p>So this is a module, called <strong>tut1</strong> that contains a
function called <strong>fac&gt;</strong>, which takes one argument,
<strong>N</strong>.</p><p>The first part says that the factorial of 1 is 1.:</p><pre><code class="">
fac(1) -&gt;
    1;</code></pre><p>Notice that this part ends with a semicolon ";" that indicates
that there is more of the function <strong>fac&gt;</strong> to come.</p><p>The second part says that the factorial of N is N multiplied
by the factorial of N - 1:</p><pre><code class="">
fac(N) -&gt;
    N * fac(N - 1).</code></pre><p>Notice that this part ends with a "." saying that there are
no more parts of this function.</p><p>Compile the file:</p><pre>
5&gt; <span class="input">c(tut1).</span>
{ok,tut1}</pre><p>And now calculate the factorial of 4.</p><pre>
6&gt; <span class="input">tut1:fac(4).</span>
24</pre><p>Here the function <strong>fac&gt;</strong> in module <strong>tut1</strong> is called
with argument <strong>4</strong>.</p><p>A function can have many arguments. Let us expand the module
<strong>tut1</strong> with the function to multiply two numbers:</p><pre><code class="">
-module(tut1).
-export([fac/1, mult/2]).

fac(1) -&gt;
    1;
fac(N) -&gt;
    N * fac(N - 1).

mult(X, Y) -&gt;
    X * Y.</code></pre><p>Notice that it is also required to expand the <strong>-export</strong> line
with the information that there is another function <strong>mult</strong>
with two arguments.</p><p>Compile:</p><pre>
7&gt; <span class="input">c(tut1).</span>
{ok,tut1}</pre><p>Try out the new function <strong>mult</strong>:</p><pre>
8&gt; <span class="input">tut1:mult(3,4).</span>
12</pre><p>In this example the numbers are integers and the arguments
in the functions in the code <strong>N</strong>, <strong>X</strong>, and <strong>Y</strong> are
called variables. Variables must start with a capital letter
(see
<a href="./expressions">Variables</a>).
Examples of variables are
<strong>Number</strong>, <strong>ShoeSize</strong>, and <strong>Age</strong>.</p><h3>Atoms</h3><p>Atom is another data type in Erlang. Atoms start with a small
letter (see
<a href="./data_types">Atom</a>),
for example, <strong>charles</strong>,
<strong>centimeter</strong>, and <strong>inch</strong>. Atoms are simply names, nothing
else. They are not like variables, which can have a value.</p><p>Enter the next program in a file named <strong>tut2.erl</strong>). It can be
useful for converting from inches to centimeters and conversely:</p><pre><code class="">
-module(tut2).
-export([convert/2]).

convert(M, inch) -&gt;
    M / 2.54;

convert(N, centimeter) -&gt;
    N * 2.54.</code></pre><p>Compile:</p><pre>
9&gt; <span class="input">c(tut2).</span>
{ok,tut2}
</pre><p>Test:</p><pre>
10&gt; <span class="input">tut2:convert(3, inch).</span>
1.1811023622047243
11&gt; <span class="input">tut2:convert(7, centimeter).</span>
17.78</pre><p>Notice the introduction of decimals (floating point numbers)
without any explanation. Hopefully you can cope with that.</p><p>Let us see what happens if something other than <strong>centimeter</strong> or
<strong>inch</strong> is entered in the <strong>convert</strong> function:</p><pre>
12&gt; <span class="input">tut2:convert(3, miles).</span>
** exception error: no function clause matching tut2:convert(3,miles) (tut2.erl, line 4)</pre><p>The two parts of the <strong>convert</strong> function are called its
clauses. As shown, <strong>miles</strong> is not part of either of
the clauses. The Erlang system cannot <em>match</em> either of
the clauses so an error message <strong>function_clause</strong> is returned.
The shell formats the error message nicely, but the error tuple
is saved in the shell's history list and can be output by the shell
command <strong>v/1</strong>:</p><pre>
13&gt; <span class="input">v(12).</span>
{'EXIT',{function_clause,[{tut2,convert,
                                [3,miles],
                                [{file,"tut2.erl"},{line,4}]},
                          {erl_eval,do_apply,6,
                                    [{file,"erl_eval.erl"},{line,677}]},
                          {shell,exprs,7,[{file,"shell.erl"},{line,687}]},
                          {shell,eval_exprs,7,[{file,"shell.erl"},{line,642}]},
                          {shell,eval_loop,3,
                                 [{file,"shell.erl"},{line,627}]}]}}</pre><h3>Tuples</h3><p>Now the <strong>tut2</strong> program is hardly good programming style.
Consider:</p><pre><code class="">
tut2:convert(3, inch).</code></pre><p>Does this mean that 3 is in inches? Or does it mean that 3 is
in centimeters
and is to be converted to inches? Erlang has a way to group
things together to make things more understandable. These are called
<em>tuples</em> and are surrounded by curly brackets, "{" and "}".</p><p>So, <strong>{inch,3}</strong> denotes 3 inches and
<strong>{centimeter,5}</strong> denotes 5 centimeters. Now let us write a
new program that converts centimeters to inches and conversely.
Enter the following code in a file called <strong>tut3.erl</strong>):</p><pre><code class="">
-module(tut3).
-export([convert_length/1]).

convert_length({centimeter, X}) -&gt;
    {inch, X / 2.54};
convert_length({inch, Y}) -&gt;
    {centimeter, Y * 2.54}.</code></pre><p>Compile and test:</p><pre>
14&gt; <span class="input">c(tut3).</span>
{ok,tut3}
15&gt; <span class="input">tut3:convert_length({inch, 5}).</span>
{centimeter,12.7}
16&gt; <span class="input">tut3:convert_length(tut3:convert_length({inch, 5})).</span>
{inch,5.0}</pre><p>Notice on line 16 that 5 inches is converted to centimeters and back
again and reassuringly get back to the original value. That is,
the argument to a function can be the result of another function.
Consider how line 16 (above) works.
The argument given to the function <strong>{inch,5}</strong> is first
matched against the first head clause of <strong>convert_length</strong>,
that is, <strong>convert_length({centimeter,X})</strong>. It can be seen
that <strong>{centimeter,X}</strong> does not match <strong>{inch,5}</strong>
(the head is the bit before the "-&gt;"). This having failed,
let us try
the head of the next clause that is, <strong>convert_length({inch,Y})</strong>.
This matches, and <strong>Y</strong> gets the value 5.</p><p>Tuples can have more than two parts, in fact
as many parts as you want, and contain any valid Erlang
<em>term</em>. For example, to represent the temperature of
various cities of the world:</p><pre><code class="">
{moscow, {c, -10}}
{cape_town, {f, 70}}
{paris, {f, 28}}</code></pre><p>Tuples have a fixed number of items in them. Each item in a
tuple is called an <em>element</em>. In the tuple
<strong>{moscow,{c,-10}}</strong>, element 1 is <strong>moscow</strong> and element
2 is <strong>{c,-10}</strong>. Here <strong>c</strong> represents Celsius and
<strong>f</strong> Fahrenheit.</p><h3>Lists</h3><p>Whereas tuples group things together, it is also needed to
represent lists of things. Lists in Erlang are surrounded by
square brackets, "[" and "]". For example, a list of the
temperatures of various cities in the world can be:</p><pre><code class="">
[{moscow, {c, -10}}, {cape_town, {f, 70}}, {stockholm, {c, -4}},
 {paris, {f, 28}}, {london, {f, 36}}]</code></pre><p>Notice that this list was so long that it did not fit on one line.
This does not matter, Erlang allows line breaks at all "sensible
places" but not, for example, in the middle of atoms, integers,
and others.</p><p>A useful way of looking at parts of lists, is by using "|".
This is best explained by an example using the shell:</p><pre>
17&gt; <span class="input">[First |TheRest] = [1,2,3,4,5].</span>
[1,2,3,4,5]
18&gt; <span class="input">First.</span>
1
19&gt; <span class="input">TheRest.</span>
[2,3,4,5]</pre><p>To separate the first elements of the list from the rest of the
list, <strong>|</strong> is used. <strong>First</strong> has got value 1 and
<strong>TheRest</strong> has got the value [2,3,4,5].</p><p>Another example:</p><pre>
20&gt; <span class="input">[E1, E2 | R] = [1,2,3,4,5,6,7].</span>
[1,2,3,4,5,6,7]
21&gt; <span class="input">E1.</span>
1
22&gt; <span class="input">E2.</span>
2
23&gt; <span class="input">R.</span>
[3,4,5,6,7]</pre><p>Here you see the use of <strong>|</strong> to get the first two elements from
the list. If you try to get more elements from the list
than there are elements in the list, an error is returned. Notice
also the special case of the list with no elements, []:</p><pre>
24&gt; <span class="input">[A, B | C] = [1, 2].</span>
[1,2]
25&gt; <span class="input">A.</span>
1
26&gt; <span class="input">B.</span>
2
27&gt; <span class="input">C.</span>
[]</pre><p>In the previous examples, new variable names are used, instead of
reusing the old ones: <strong>First</strong>, <strong>TheRest</strong>, <strong>E1</strong>,
<strong>E2</strong>, <strong>R</strong>, <strong>A</strong>, <strong>B</strong>, and <strong>C</strong>. The reason
for this is that a variable can only be given a value once in its
context (scope). More about this later.</p><p>The following example shows how to find the length of a list.
Enter the following code in a file named <strong>tut4.erl</strong>):</p><pre><code class="">
-module(tut4).

-export([list_length/1]).

list_length([]) -&gt;
    0;    
list_length([First | Rest]) -&gt;
    1 + list_length(Rest).</code></pre><p>Compile and test:</p><pre>
28&gt; <span class="input">c(tut4).</span>
{ok,tut4}
29&gt; <span class="input">tut4:list_length([1,2,3,4,5,6,7]).</span>
7</pre><p>Explanation:</p><pre><code class="">
list_length([]) -&gt;
    0;</code></pre><p>The length of an empty list is obviously 0.</p><pre><code class="">
list_length([First | Rest]) -&gt;
    1 + list_length(Rest).</code></pre><p>The length of a list with the first element <strong>First</strong> and
the remaining elements <strong>Rest</strong> is 1 + the length of
<strong>Rest</strong>.</p><p>(Advanced readers only: This is not tail recursive, there is a
better way to write this function.)</p><p>In general, tuples are used where "records"
or "structs" are used in other languages. Also, lists are used when
representing things with varying sizes, that is, where
linked lists are used in other languages.</p><p>Erlang does not have a string data type. Instead, strings can be
represented by lists of Unicode characters. This implies for example that
the list <strong>[97,98,99]</strong> is equivalent to "abc". The Erlang shell is
"clever" and guesses what list you  mean and outputs it
in what it thinks is the most appropriate form, for example:</p><pre>
30&gt; <span class="input">[97,98,99].</span>
"abc"</pre><h3>Maps</h3><p>Maps are a set of key to value associations. These associations
are encapsulated with "#{" and "}". To create an association
from "key" to value 42:</p><pre><code class="">
&gt; #{ "key" =&gt; 42 }.
#{"key" =&gt; 42}</code></pre><p>Let us jump straight into the deep end with an example using some
interesting features.</p><p>The following example shows how to calculate alpha blending
using maps to reference color and alpha channels. Enter the code
in a file named <strong>color.erl</strong>):</p><pre><code class="">
-module(color).

-export([new/4, blend/2]).

-define(is_channel(V), (is_float(V) andalso V &gt;= 0.0 andalso V =&lt; 1.0)).

new(R,G,B,A) when ?is_channel(R), ?is_channel(G),
                  ?is_channel(B), ?is_channel(A) -&gt;
    #{red =&gt; R, green =&gt; G, blue =&gt; B, alpha =&gt; A}.

blend(Src,Dst) -&gt;
    blend(Src,Dst,alpha(Src,Dst)).

blend(Src,Dst,Alpha) when Alpha &gt; 0.0 -&gt;
    Dst#{
        red   := red(Src,Dst) / Alpha,
        green := green(Src,Dst) / Alpha,
        blue  := blue(Src,Dst) / Alpha,
        alpha := Alpha
    };
blend(_,Dst,_) -&gt;
    Dst#{
        red   := 0.0,
        green := 0.0,
        blue  := 0.0,
        alpha := 0.0
    }.

alpha(#{alpha := SA}, #{alpha := DA}) -&gt;
    SA + DA*(1.0 - SA).

red(#{red := SV, alpha := SA}, #{red := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).
green(#{green := SV, alpha := SA}, #{green := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).
blue(#{blue := SV, alpha := SA}, #{blue := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).</code></pre><p>Compile and test:</p><pre>
&gt; <span class="input">c(color).</span>
{ok,color}
&gt; <span class="input">C1 = color:new(0.3,0.4,0.5,1.0).</span>
#{alpha =&gt; 1.0,blue =&gt; 0.5,green =&gt; 0.4,red =&gt; 0.3}
&gt; <span class="input">C2 = color:new(1.0,0.8,0.1,0.3).</span>
#{alpha =&gt; 0.3,blue =&gt; 0.1,green =&gt; 0.8,red =&gt; 1.0}
&gt; <span class="input">color:blend(C1,C2).</span>
#{alpha =&gt; 1.0,blue =&gt; 0.5,green =&gt; 0.4,red =&gt; 0.3}
&gt; <span class="input">color:blend(C2,C1).</span>
#{alpha =&gt; 1.0,blue =&gt; 0.38,green =&gt; 0.52,red =&gt; 0.51}
</pre><p>This example warrants some explanation:</p><pre><code class="">
-define(is_channel(V), (is_float(V) andalso V &gt;= 0.0 andalso V =&lt; 1.0)).</code></pre><p>First a macro <strong>is_channel</strong> is defined to help with the
guard tests. This is only here for convenience and to reduce
syntax cluttering. For more information about macros, see
<a href="./macros"> The Preprocessor</a>.
</p><pre><code class="">
new(R,G,B,A) when ?is_channel(R), ?is_channel(G),
                  ?is_channel(B), ?is_channel(A) -&gt;
    #{red =&gt; R, green =&gt; G, blue =&gt; B, alpha =&gt; A}.</code></pre><p>The function <strong>new/4</strong> creates a new map term and lets the keys
<strong>red</strong>, <strong>green</strong>, <strong>blue</strong>, and <strong>alpha</strong> be
associated with an initial value. In this case, only float
values between and including 0.0 and 1.0 are allowed, as ensured
by the <strong>?is_channel/1</strong> macro for each argument. Only the
<strong>=&gt;</strong> operator is allowed when creating a new map.
</p><p>By calling <strong>blend/2</strong> on any color term created by
<strong>new/4</strong>, the resulting color can be calculated as
determined by the two map terms.
</p><p>The first thing <strong>blend/2</strong> does is to calculate the
resulting alpha channel:
</p><pre><code class="">
alpha(#{alpha := SA}, #{alpha := DA}) -&gt;
    SA + DA*(1.0 - SA).</code></pre><p>The value associated with key <strong>alpha</strong> is fetched for both
arguments using the <strong>:=</strong> operator. The other keys in the
map are ignored, only the key <strong>alpha</strong> is required and
checked for.
</p><p>This is also the case for functions <strong>red/2</strong>,
<strong>blue/2</strong>, and <strong>green/2</strong>.</p><pre><code class="">
red(#{red := SV, alpha := SA}, #{red := DV, alpha := DA}) -&gt;
    SV*SA + DV*DA*(1.0 - SA).</code></pre><p>The difference here is that a check is made for two keys in
each map argument. The other keys are ignored.
</p><p>Finally, let us return the resulting color in <strong>blend/3</strong>:
</p><pre><code class="">
blend(Src,Dst,Alpha) when Alpha &gt; 0.0 -&gt;
    Dst#{
        red   := red(Src,Dst) / Alpha,
        green := green(Src,Dst) / Alpha,
        blue  := blue(Src,Dst) / Alpha,
        alpha := Alpha
    };</code></pre><p>The <strong>Dst</strong> map is updated with new channel values. The
syntax for updating an existing key with a new value is with the
<strong>:=</strong> operator.
</p><h3>Standard Modules and Manual Pages</h3><p>Erlang has many standard modules to help you do things. For
example, the module <strong>io</strong> contains many functions that help
in doing formatted input/output. To look up information about
standard modules, the command <strong>erl -man</strong> can be used at the
operating shell or command prompt (the same place as you started
<strong>erl</strong>). Try the operating system shell command:</p><pre>
% <span class="input">erl -man io</span>
ERLANG MODULE DEFINITION                                    io(3)

MODULE
     io - Standard I/O Server Interface Functions

DESCRIPTION
     This module provides an  interface  to  standard  Erlang  IO
     servers. The output functions all return ok if they are suc-
     ...</pre><p>If this does not work on your system, the documentation is
included as HTML in the Erlang/OTP release. You can also read
the documentation as HTML or download it as PDF from either of
the sites www.erlang.se (commercial Erlang) or www.erlang.org
(open source). For example, for Erlang/OTP release R9B:</p><pre><code class="">
http://www.erlang.org/doc/r9b/doc/index.html</code></pre><h3>Writing Output to a Terminal</h3><p>It is nice to be able to do formatted output in examples, so
the next example shows a simple way to use the <strong>io:format</strong>
function. Like all other exported functions, you can test the
<strong>io:format</strong> function in the shell:</p><pre>
31&gt; <span class="input">io:format("hello world~n", []).</span>
hello world
ok
32&gt; <span class="input">io:format("this outputs one Erlang term: ~w~n", [hello]).</span>
this outputs one Erlang term: hello
ok
33&gt; <span class="input">io:format("this outputs two Erlang terms: ~w~w~n", [hello, world]).</span>
this outputs two Erlang terms: helloworld
ok
34&gt; <span class="input">io:format("this outputs two Erlang terms: ~w ~w~n", [hello, world]).</span>
this outputs two Erlang terms: hello world
ok</pre><p>The function <strong>format/2</strong> (that is, <strong>format</strong> with two
arguments) takes two lists. The first one is nearly always a list
written between " ". This list is printed out as it is,
except that each ~w is replaced by a term taken in order from
the second list. Each ~n is replaced by a new line.
The <strong>io:format/2</strong> function itself returns the atom <strong>ok</strong>
if everything goes as planned. Like other functions in Erlang, it
crashes if an error occurs. This is not a fault in Erlang, it is
a deliberate policy. Erlang has sophisticated mechanisms to
handle errors which are shown later. As an exercise, try to
make <strong>io:format</strong> crash, it should not be difficult. But
notice that although <strong>io:format</strong> crashes, the Erlang shell
itself does not crash.</p><h3>A Larger Example</h3><p>Now for a larger example to consolidate what you have learnt so
far. Assume that you have a list of temperature readings from a number
of cities in the world. Some of them are in Celsius
and some in Fahrenheit (as in the previous list). First let us
convert them all to Celsius, then let us print the data neatly.</p><pre><code class="">
%% This module is in file tut5.erl

-module(tut5).
-export([format_temps/1]).

%% Only this function is exported
format_temps([])-&gt;                        % No output for an empty list
    ok;
format_temps([City | Rest]) -&gt;
    print_temp(convert_to_celsius(City)),
    format_temps(Rest).

convert_to_celsius({Name, {c, Temp}}) -&gt;  % No conversion needed
    {Name, {c, Temp}};
convert_to_celsius({Name, {f, Temp}}) -&gt;  % Do the conversion
    {Name, {c, (Temp - 32) * 5 / 9}}.

print_temp({Name, {c, Temp}}) -&gt;
    io:format("~-15w ~w c~n", [Name, Temp]).</code></pre><pre>
35&gt; <span class="input">c(tut5).</span>
{ok,tut5}
36&gt; <span class="input">tut5:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
moscow          -10 c
cape_town       21.11111111111111 c
stockholm       -4 c
paris           -2.2222222222222223 c
london          2.2222222222222223 c
ok</pre><p>Before looking at how this program works, notice that
a few comments are added to the code. A comment starts with a
%-character and goes on to the end of the line. Notice also that
the <strong>-export([format_temps/1]).</strong> line only includes
the function <strong>format_temps/1</strong>. The other functions are
<em>local</em> functions, that is, they are not visible from outside
the module <strong>tut5</strong>.</p><p>Notice also that when testing the program from the shell,
the input is spread over two lines as the line was too long.</p><p>When <strong>format_temps</strong> is called the first time, <strong>City</strong>
gets the value <strong>{moscow,{c,-10}}</strong> and <strong>Rest</strong> is
the rest of the list. So the function
<strong>print_temp(convert_to_celsius({moscow,{c,-10}}))</strong> is called.</p><p>Here is a function call as
<strong>convert_to_celsius({moscow,{c,-10}})</strong> as the argument to
the function <strong>print_temp</strong>. When function calls are <em>nested</em>
like this, they execute (evaluate) from the inside out.
That is, first <strong>convert_to_celsius({moscow,{c,-10}})</strong> is evaluated,
which gives the value <strong>{moscow,{c,-10}}</strong> as the temperature
is already in Celsius. Then <strong>print_temp({moscow,{c,-10}})</strong>
is evaluated.
The function <strong>convert_to_celsius</strong> works in a similar way to
the <strong>convert_length</strong> function in the previous example.</p><p><strong>print_temp</strong> simply calls <strong>io:format</strong> in a similar way
to what has been described above. Notice that ~-15w says to print
the "term" with a field length (width) of 15 and left justify it.
(see the <a href="../stdlib/io#fwrite/1">io(3)</a>) manual page in STDLIB.</p><p>Now <strong>format_temps(Rest)</strong> is called with the rest of the list
as an argument. This way of doing things is similar to the loop
constructs in other languages. (Yes, this is recursion, but do not
let that worry you.) So the same <strong>format_temps</strong> function is
called again, this time <strong>City</strong> gets the value
<strong>{cape_town,{f,70}}</strong> and the same procedure is repeated as
before. This is done until the list becomes empty, that is [],
which causes the first clause <strong>format_temps([])</strong> to match.
This simply returns (results in) the atom <strong>ok</strong>, so
the program ends.</p><h3>Matching, Guards, and Scope of Variables</h3><p>It can be useful to find the maximum and minimum temperature
in lists like this. Before extending the program to do this,
let us look at functions for finding the maximum value of
the elements in a list:</p><pre><code class="">
-module(tut6).
-export([list_max/1]).

list_max([Head|Rest]) -&gt;
   list_max(Rest, Head).

list_max([], Res) -&gt;
    Res;
list_max([Head|Rest], Result_so_far) when Head &gt; Result_so_far -&gt;
    list_max(Rest, Head);
list_max([Head|Rest], Result_so_far)  -&gt;
    list_max(Rest, Result_so_far).</code></pre><pre>
37&gt; <span class="input">c(tut6).</span>
{ok,tut6}
38&gt; <span class="input">tut6:list_max([1,2,3,4,5,7,4,3,2,1]).</span>
7</pre><p>First notice that two functions have the same name,
<strong>list_max</strong>. However, each of these takes a different number
of arguments (parameters). In Erlang these are regarded as
completely different functions. Where you need to distinguish
between these functions, you write Name/Arity, where
Name is the function name and Arity is
the number of arguments, in this case <strong>list_max/1</strong> and
<strong>list_max/2</strong>.</p><p>In this example you walk through a list "carrying" a
value, in this case <strong>Result_so_far</strong>.
<strong>list_max/1</strong> simply assumes that the max value of the list
is the head of the list and calls <strong>list_max/2</strong> with the rest
of the list and the value of the head of the list. In the above
this would be <strong>list_max([2,3,4,5,7,4,3,2,1],1)</strong>. If you tried
to use <strong>list_max/1</strong> with an empty list or tried to use it
with something that is not a list at all, you would cause an error.
Notice that the Erlang philosophy is not to handle errors of this
type in the function they occur, but to do so elsewhere. More
about this later.</p><p>In <strong>list_max/2</strong>, you walk down the list and use <strong>Head</strong>
instead of <strong>Result_so_far</strong> when <strong>Head</strong> &gt;
<strong>Result_so_far</strong>. <strong>when</strong> is a special word used before
the -&gt; in the function to say that you only use this part
of the function if the test that follows is true. A test
of this type is called <em>guard</em>. If the guard is false (that is,
the guard fails), the next part of the function is tried. In this
case, if <strong>Head</strong> is not greater than <strong>Result_so_far</strong>, then
it must be smaller or equal to it. This means that a guard on
the next part of the function is not needed.</p><p>Some useful operators in guards are:
</p><ul><li>&lt; less than</li><li>&gt; greater than</li><li>== equal</li><li>&gt;= greater or equal</li><li>=&lt; less or equal</li><li>/= not equal</li></ul><p>(see <a href="./expressions">Guard Sequences</a>).</p><p>To change the above program to one that works out the minimum
value of the element in a list, you only need to
write &lt; instead of &gt;. (But it would be wise to change
the name of the function to <strong>list_min</strong>.)</p><p>Earlier it was mentioned that a variable can only be
given a value once in its scope. In the above you see
that <strong>Result_so_far</strong> is given several values. This is
OK since every time you call <strong>list_max/2</strong> you create a new
scope and one can regard <strong>Result_so_far</strong> as a
different variable in each scope.</p><p>Another way of creating and giving a variable a value is by using
the match operator = . So if you write <strong>M = 5</strong>, a variable
called <strong>M</strong> is created with the value 5. If, in
the same scope, you then write <strong>M = 6</strong>, an error is returned. Try
this out in the shell:</p><pre>
39&gt; <span class="input">M = 5.</span>
5
40&gt; <span class="input">M = 6.</span>
** exception error: no match of right hand side value 6
41&gt; <span class="input">M = M + 1.</span>
** exception error: no match of right hand side value 6
42&gt; <span class="input">N = M + 1.</span>
6</pre><p>The use of the match operator is particularly useful for pulling
apart Erlang terms and creating new ones.</p><pre>
43&gt; <span class="input">{X, Y} = {paris, {f, 28}}.</span>
{paris,{f,28}}
44&gt; <span class="input">X.</span>
paris
45&gt; <span class="input">Y.</span>
{f,28}</pre><p>Here <strong>X</strong> gets the value <strong>paris</strong> and
<strong>Y</strong><strong>{f,28}</strong>.</p><p>If you try to do the same again with another city,
an error is returned:</p><pre>
46&gt; <span class="input">{X, Y} = {london, {f, 36}}.</span>
** exception error: no match of right hand side value {london,{f,36}}</pre><p>Variables can also be used to improve the readability of
programs. For example, in function <strong>list_max/2</strong> above,
you can write:</p><pre><code class="">
list_max([Head|Rest], Result_so_far) when Head &gt; Result_so_far -&gt;
    New_result_far = Head,
    list_max(Rest, New_result_far);</code></pre><p>This is possibly a little clearer.</p><h3>More About Lists</h3><p>Remember that the | operator can be used to get the head of a
list:</p><pre>
47&gt; <span class="input">[M1|T1] = [paris, london, rome].</span>
[paris,london,rome]
48&gt; <span class="input">M1.</span>
paris
49&gt; <span class="input">T1.</span>
[london,rome]</pre><p>The | operator can also be used to add a head to a list:</p><pre>
50&gt; <span class="input">L1 = [madrid | T1].</span>
[madrid,london,rome]
51&gt; <span class="input">L1.</span>
[madrid,london,rome]</pre><p>Now an example of this when working with lists - reversing
the order of a list:</p><pre><code class="">
-module(tut8).

-export([reverse/1]).

reverse(List) -&gt;
    reverse(List, []).

reverse([Head | Rest], Reversed_List) -&gt;
    reverse(Rest, [Head | Reversed_List]);
reverse([], Reversed_List) -&gt;
    Reversed_List.</code></pre><pre>
52&gt; <span class="input">c(tut8).</span>
{ok,tut8}
53&gt; <span class="input">tut8:reverse([1,2,3]).</span>
[3,2,1]</pre><p>Consider how <strong>Reversed_List</strong> is built. It starts as [],
then successively the heads are taken off of the list to be reversed
and added to the the <strong>Reversed_List</strong>, as shown in
the following:</p><pre><code class="">
reverse([1|2,3], []) =&gt;
    reverse([2,3], [1|[]])

reverse([2|3], [1]) =&gt;
    reverse([3], [2|[1])

reverse([3|[]], [2,1]) =&gt;
    reverse([], [3|[2,1]])

reverse([], [3,2,1]) =&gt;
    [3,2,1]</code></pre><p>The module <strong>lists</strong> contains many functions for
manipulating lists, for example, for reversing them. So before
writing a list-manipulating function it is a good idea to check
if one not already is written for you
(see the <a href="./lists">lists(3)</a>
manual page in STDLIB).</p><p>Now let us get back to the cities and temperatures, but take a more
structured approach this time. First let us convert the whole list
to Celsius as follows:</p><pre><code class="">
-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    convert_list_to_c(List_of_cities).

convert_list_to_c([{Name, {f, F}} | Rest]) -&gt;
    Converted_City = {Name, {c, (F -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].</code></pre><p>Test the function:</p><pre>
54&gt; <span class="input">c(tut7).</span>
{ok, tut7}.
55&gt; <span class="input">tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
[{moscow,{c,-10}},
 {cape_town,{c,21.11111111111111}},
 {stockholm,{c,-4}},
 {paris,{c,-2.2222222222222223}},
 {london,{c,2.2222222222222223}}]</pre><p>Explanation:</p><pre><code class="">
format_temps(List_of_cities) -&gt;
    convert_list_to_c(List_of_cities).</code></pre><p>Here <strong>format_temps/1</strong> calls
<strong>convert_list_to_c/1</strong>. <strong>convert_list_to_c/1</strong> takes off
the head of the <strong>List_of_cities</strong>, converts it to Celsius if
needed. The | operator is used to add the (maybe) converted
to the converted rest of the list:</p><pre><code class="">
[Converted_City | convert_list_to_c(Rest)];</code></pre><p>or:</p><pre><code class="">
[City | convert_list_to_c(Rest)];</code></pre><p>This is done until the end of the list is reached, that is,
the list is empty:</p><pre><code class="">
convert_list_to_c([]) -&gt;
    [].</code></pre><p>Now when the list is converted, a function to print it is added:</p><pre><code class="">
-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    Converted_List = convert_list_to_c(List_of_cities),
    print_temp(Converted_List).

convert_list_to_c([{Name, {f, F}} | Rest]) -&gt;
    Converted_City = {Name, {c, (F -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].

print_temp([{Name, {c, Temp}} | Rest]) -&gt;
    io:format("~-15w ~w c~n", [Name, Temp]),
    print_temp(Rest);
print_temp([]) -&gt;
    ok.</code></pre><pre>
56&gt; <span class="input">c(tut7).</span>
{ok,tut7}
57&gt; <span class="input">tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
moscow          -10 c
cape_town       21.11111111111111 c
stockholm       -4 c
paris           -2.2222222222222223 c
london          2.2222222222222223 c
ok</pre><p>Now a function has to be added to find the cities with
the maximum and minimum temperatures. The following program is not
the most efficient way of doing this as you walk through the list
of cities four times. But it is better to first strive for
clarity and correctness and to make programs efficient only if
needed.</p><pre><code class="">
-module(tut7).
-export([format_temps/1]).

format_temps(List_of_cities) -&gt;
    Converted_List = convert_list_to_c(List_of_cities),
    print_temp(Converted_List),
    {Max_city, Min_city} = find_max_and_min(Converted_List),
    print_max_and_min(Max_city, Min_city).

convert_list_to_c([{Name, {f, Temp}} | Rest]) -&gt;
    Converted_City = {Name, {c, (Temp -32)* 5 / 9}},
    [Converted_City | convert_list_to_c(Rest)];

convert_list_to_c([City | Rest]) -&gt;
    [City | convert_list_to_c(Rest)];

convert_list_to_c([]) -&gt;
    [].

print_temp([{Name, {c, Temp}} | Rest]) -&gt;
    io:format("~-15w ~w c~n", [Name, Temp]),
    print_temp(Rest);
print_temp([]) -&gt;
    ok.

find_max_and_min([City | Rest]) -&gt;
    find_max_and_min(Rest, City, City).

find_max_and_min([{Name, {c, Temp}} | Rest], 
         {Max_Name, {c, Max_Temp}}, 
         {Min_Name, {c, Min_Temp}}) -&gt;
    if 
        Temp &gt; Max_Temp -&gt;
            Max_City = {Name, {c, Temp}};           % Change
        true -&gt; 
            Max_City = {Max_Name, {c, Max_Temp}} % Unchanged
    end,
    if
         Temp &lt; Min_Temp -&gt;
            Min_City = {Name, {c, Temp}};           % Change
        true -&gt; 
            Min_City = {Min_Name, {c, Min_Temp}} % Unchanged
    end,
    find_max_and_min(Rest, Max_City, Min_City);

find_max_and_min([], Max_City, Min_City) -&gt;
    {Max_City, Min_City}.

print_max_and_min({Max_name, {c, Max_temp}}, {Min_name, {c, Min_temp}}) -&gt;
    io:format("Max temperature was ~w c in ~w~n", [Max_temp, Max_name]),
    io:format("Min temperature was ~w c in ~w~n", [Min_temp, Min_name]).</code></pre><pre>
58&gt; <span class="input">c(tut7).</span>
{ok, tut7}
59&gt; <span class="input">tut7:format_temps([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
moscow          -10 c
cape_town       21.11111111111111 c
stockholm       -4 c
paris           -2.2222222222222223 c
london          2.2222222222222223 c
Max temperature was 21.11111111111111 c in cape_town
Min temperature was -10 c in moscow
ok</pre><h3>If and Case</h3><p>The function <strong>find_max_and_min</strong> works out the maximum and
minimum temperature. A new construct, <strong>if</strong>, is introduced here.
If works as follows:</p><pre><code class="">
if
    Condition 1 -&gt;
        Action 1;
    Condition 2 -&gt;
        Action 2;
    Condition 3 -&gt;
        Action 3;
    Condition 4 -&gt;
        Action 4
end</code></pre><p>Notice that there is no ";" before <strong>end</strong>. Conditions do
the same as guards, that is, tests that succeed or fail. Erlang
starts at the top
and tests until it finds a condition that succeeds. Then it evaluates
(performs) the action following the condition and ignores all
other conditions and actions before the <strong>end</strong>. If no
condition matches, a run-time failure occurs. A condition
that always succeeds is the atom <strong>true</strong>. This is
often used last in an <strong>if</strong>, meaning, do the action following
the <strong>true</strong> if all other conditions have failed.</p><p>The following is a short program to show the workings of
<strong>if</strong>.</p><pre><code class="">
-module(tut9).
-export([test_if/2]).

test_if(A, B) -&gt;
    if 
        A == 5 -&gt;
            io:format("A == 5~n", []),
            a_equals_5;
        B == 6 -&gt;
            io:format("B == 6~n", []),
            b_equals_6;
        A == 2, B == 3 -&gt;                      %That is A equals 2 and B equals 3
            io:format("A == 2, B == 3~n", []),
            a_equals_2_b_equals_3;
        A == 1 ; B == 7 -&gt;                     %That is A equals 1 or B equals 7
            io:format("A == 1 ; B == 7~n", []),
            a_equals_1_or_b_equals_7
    end.</code></pre><p>Testing this program gives:</p><pre>
60&gt; <span class="input">c(tut9).</span>
{ok,tut9}
61&gt; <span class="input">tut9:test_if(5,33).</span>
A == 5
a_equals_5
62&gt; <span class="input">tut9:test_if(33,6).</span>
B == 6
b_equals_6
63&gt; <span class="input">tut9:test_if(2, 3).</span>
A == 2, B == 3
a_equals_2_b_equals_3
64&gt; <span class="input">tut9:test_if(1, 33).</span>
A == 1 ; B == 7
a_equals_1_or_b_equals_7
65&gt; <span class="input">tut9:test_if(33, 7).</span>
A == 1 ; B == 7
a_equals_1_or_b_equals_7
66&gt; <span class="input">tut9:test_if(33, 33).</span>
** exception error: no true branch found when evaluating an if expression
     in function  tut9:test_if/2 (tut9.erl, line 5)</pre><p>Notice that <strong>tut9:test_if(33,33)</strong> does not cause any
condition to succeed. This leads to the run time error
<strong>if_clause</strong>, here nicely formatted by the shell. See
<a href="./expressions">Guard Sequences</a>
for details of the many guard tests available.</p><p><strong>case</strong> is another construct in Erlang. Recall that the
<strong>convert_length</strong> function was written as:</p><pre><code class="">
convert_length({centimeter, X}) -&gt;
    {inch, X / 2.54};
convert_length({inch, Y}) -&gt;
    {centimeter, Y * 2.54}.</code></pre><p>The same program can also be written as:</p><pre><code class="">
-module(tut10).
-export([convert_length/1]).

convert_length(Length) -&gt;
    case Length of
        {centimeter, X} -&gt;
            {inch, X / 2.54};
        {inch, Y} -&gt;
            {centimeter, Y * 2.54}
    end.</code></pre><pre>
67&gt; <span class="input">c(tut10).</span>
{ok,tut10}
68&gt; <span class="input">tut10:convert_length({inch, 6}).</span>
{centimeter,15.24}
69&gt; <span class="input">tut10:convert_length({centimeter, 2.5}).</span>
{inch,0.984251968503937}</pre><p>Both <strong>case</strong> and <strong>if</strong> have <em>return values</em>, that is,
in the above example <strong>case</strong> returned
either <strong>{inch,X/2.54}</strong> or <strong>{centimeter,Y*2.54}</strong>.
The behaviour of <strong>case</strong> can also be modified by using guards.
The following example clarifies this. It
tells us the length of a month, given the year.
The year must be known, since February has 29 days in a leap year.</p><pre><code class="">
-module(tut11).
-export([month_length/2]).

month_length(Year, Month) -&gt;
    %% All years divisible by 400 are leap
    %% Years divisible by 100 are not leap (except the 400 rule above)
    %% Years divisible by 4 are leap (except the 100 rule above)
    Leap = if
        trunc(Year / 400) * 400 == Year -&gt;
            leap;
        trunc(Year / 100) * 100 == Year -&gt;
            not_leap;
        trunc(Year / 4) * 4 == Year -&gt;
            leap;
        true -&gt;
            not_leap
    end,  
    case Month of
        sep -&gt; 30;
        apr -&gt; 30;
        jun -&gt; 30;
        nov -&gt; 30;
        feb when Leap == leap -&gt; 29;
        feb -&gt; 28;
        jan -&gt; 31;
        mar -&gt; 31;
        may -&gt; 31;
        jul -&gt; 31;
        aug -&gt; 31;
        oct -&gt; 31;
        dec -&gt; 31
    end.</code></pre><pre>
70&gt; <span class="input">c(tut11).</span>
{ok,tut11}
71&gt; <span class="input">tut11:month_length(2004, feb).</span>
29
72&gt; <span class="input">tut11:month_length(2003, feb).</span>
28
73&gt; <span class="input">tut11:month_length(1947, aug).</span>
31</pre><h3>Built-In Functions (BIFs)</h3><p>BIFs are functions that for some reason are
built-in to the Erlang virtual machine. BIFs often implement
functionality that is impossible or is too
inefficient to implement in Erlang. Some BIFs can be called
using the function name only but they are by default belonging
to the <strong>erlang</strong> module. For example, the call to the
BIF <strong>trunc</strong>
below is equivalent to a call to <strong>erlang:trunc</strong>.</p><p>As shown, first it is checked if a year is leap. If a
year is divisible by 400, it is a leap year. To determine this,
first divide the year by 400 and use the BIF
<strong>trunc</strong> (more about this later) to cut off any decimals. Then
multiply by 400 again and see if the same value is returned again.
For example, year 2004:</p><pre><code class="">
2004 / 400 = 5.01
trunc(5.01) = 5
5 * 400 = 2000</code></pre><p>2000 is not the same as 2004, so 2004 is not divisible by 400.
Year 2000:</p><pre><code class="">
2000 / 400 = 5.0
trunc(5.0) = 5
5 * 400 = 2000</code></pre><p>That is, a leap year. The next two <strong>trunc</strong>-tests evaluate
if the year is divisible by 100 or 4 in the same way. The first
<strong>if</strong> returns <strong>leap</strong> or <strong>not_leap</strong>, which lands up
in the variable <strong>Leap</strong>. This variable is used in the guard
for <strong>feb</strong> in the following <strong>case</strong> that tells us how
long the month is.</p><p>This example showed the use of <strong>trunc</strong>. It is easier
to use the Erlang operator <strong>rem</strong> that gives the remainder
after division, for example:</p><pre>
74&gt; <span class="input">2004 rem 400.</span>
4</pre><p>So instead of writing:</p><pre><code class="">
trunc(Year / 400) * 400 == Year -&gt;
    leap;</code></pre><p>it can be written:</p><pre><code class="">
Year rem 400 == 0 -&gt;
    leap;</code></pre><p>There are many other BIFs such as
<strong>trunc</strong>. Only a few BIFs can be used in guards,
and you cannot use functions you have defined yourself in guards.
(see
<a href="./expressions">Guard Sequences</a>)
(For advanced readers: This is to ensure that guards do not have side
effects.) Let us play with a few of these functions in the shell:</p><pre>
75&gt; <span class="input">trunc(5.6).</span>
5
76&gt; <span class="input">round(5.6).</span>
6
77&gt; <span class="input">length([a,b,c,d]).</span>
4
78&gt; <span class="input">float(5).</span>
5.0
79&gt; <span class="input">is_atom(hello).</span>
true
80&gt; <span class="input">is_atom("hello").</span>
false
81&gt; <span class="input">is_tuple({paris, {c, 30}}).</span>
true
82&gt; <span class="input">is_tuple([paris, {c, 30}]).</span>
false</pre><p>All of these can be used in guards. Now for some BIFs that cannot be
used in guards:</p><pre>
83&gt; <span class="input">atom_to_list(hello).</span>
"hello"
84&gt; <span class="input">list_to_atom("goodbye").</span>
goodbye
85&gt; <span class="input">integer_to_list(22).</span>
"22"</pre><p>These three BIFs do conversions that would be difficult (or
impossible) to do in Erlang.</p><h3>Higher-Order Functions (Funs)</h3><p>Erlang, like most modern functional programming languages, has
higher-order functions. Here is an example using the shell:</p><pre>
86&gt; <span class="input">Xf = fun(X) -&gt; X * 2 end.</span>
#Fun&lt;erl_eval.5.123085357&gt;
87&gt; <span class="input">Xf(5).</span>
10</pre><p>Here is defined a function that doubles
the value of a number and assigned this function to a variable. Thus
<strong>Xf(5)</strong> returns value 10. Two useful functions when
working with lists are <strong>foreach</strong> and <strong>map</strong>, which are
defined as follows:</p><pre><code class="">
foreach(Fun, [First|Rest]) -&gt;
    Fun(First),
    foreach(Fun, Rest);
foreach(Fun, []) -&gt;
    ok.

map(Fun, [First|Rest]) -&gt; 
    [Fun(First)|map(Fun,Rest)];
map(Fun, []) -&gt; 
    [].</code></pre><p>These two functions are provided in the standard module
<strong>lists</strong>. <strong>foreach</strong> takes a list and applies a fun to
every element in the list. <strong>map</strong> creates a new list by
applying a fun to every element in a list. Going back to
the shell, <strong>map</strong> is used and a fun to add 3 to
every element of a list:</p><pre>
88&gt; <span class="input">Add_3 = fun(X) -&gt; X + 3 end.</span>
#Fun&lt;erl_eval.5.123085357&gt;
89&gt; <span class="input">lists:map(Add_3, [1,2,3]).</span>
[4,5,6]</pre><p>Let us (again) print the temperatures in a list of cities:</p><pre>
90&gt; <span class="input">Print_City = fun({City, {X, Temp}}) -&gt; io:format("~-15w ~w ~w~n",</span>
<span class="input">[City, X, Temp]) end.</span>
#Fun&lt;erl_eval.5.123085357&gt;
91&gt; <span class="input">lists:foreach(Print_City, [{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
moscow          c -10
cape_town       f 70
stockholm       c -4
paris           f 28
london          f 36
ok</pre><p>Let us now define a fun that can be used to go through a list
of cities and temperatures and transform them all to Celsius.</p><pre><code class="">
-module(tut13).

-export([convert_list_to_c/1]).

convert_to_c({Name, {f, Temp}}) -&gt;
    {Name, {c, trunc((Temp - 32) * 5 / 9)}};
convert_to_c({Name, {c, Temp}}) -&gt;
    {Name, {c, Temp}}.

convert_list_to_c(List) -&gt;
    lists:map(fun convert_to_c/1, List).</code></pre><pre>
92&gt; <span class="input">tut13:convert_list_to_c([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
[{moscow,{c,-10}},
 {cape_town,{c,21}},
 {stockholm,{c,-4}},
 {paris,{c,-2}},
 {london,{c,2}}]</pre><p>The <strong>convert_to_c</strong> function is the same as before, but here
it is used as a fun:</p><pre><code class="">
lists:map(fun convert_to_c/1, List)</code></pre><p>When a function defined elsewhere is used as a fun, it can be referred
to as <strong>Function/Arity</strong> (remember that <strong>Arity</strong> =
number of arguments). So in the <strong>map</strong>-call
<strong>lists:map(fun convert_to_c/1, List)</strong> is written. As shown,
<strong>convert_list_to_c</strong> becomes much shorter and easier to
understand.</p><p>The standard module <strong>lists</strong> also contains a function
<strong>sort(Fun, List)</strong> where <strong>Fun</strong> is a fun with two
arguments. This fun returns <strong>true</strong> if the first
argument is less than the second argument, or else <strong>false</strong>.
Sorting is added to the <strong>convert_list_to_c</strong>:</p><pre><code class="">
-module(tut13).

-export([convert_list_to_c/1]).

convert_to_c({Name, {f, Temp}}) -&gt;
    {Name, {c, trunc((Temp - 32) * 5 / 9)}};
convert_to_c({Name, {c, Temp}}) -&gt;
    {Name, {c, Temp}}.

convert_list_to_c(List) -&gt;
    New_list = lists:map(fun convert_to_c/1, List),
    lists:sort(fun({_, {c, Temp1}}, {_, {c, Temp2}}) -&gt;
                       Temp1 &lt; Temp2 end, New_list).</code></pre><pre>
93&gt; <span class="input">c(tut13).</span>
{ok,tut13}
94&gt; <span class="input">tut13:convert_list_to_c([{moscow, {c, -10}}, {cape_town, {f, 70}},</span>
<span class="input">{stockholm, {c, -4}}, {paris, {f, 28}}, {london, {f, 36}}]).</span>
[{moscow,{c,-10}},
 {stockholm,{c,-4}},
 {paris,{c,-2}},
 {london,{c,2}},
 {cape_town,{c,21}}]</pre><p>In <strong>sort</strong> the fun is used:</p><pre><code class="">
fun({_, {c, Temp1}}, {_, {c, Temp2}}) -&gt; Temp1 &lt; Temp2 end,</code></pre><p>Here the concept of an <em>anonymous variable</em>
"_" is introduced. This is simply shorthand for a variable that
gets a value, but the value is ignored. This can be used
anywhere suitable, not just in funs. <strong>Temp1 &lt; Temp2</strong>
returns <strong>true</strong> if <strong>Temp1</strong> is less than <strong>Temp2</strong>.</p><a name="Distributed Programming"></a><h3>Processes</h3><p>One of the main reasons for using Erlang instead of other
functional languages is Erlang's ability to handle concurrency
and distributed programming. By concurrency is meant programs
that can handle several threads of execution at the same time.
For example, modern operating systems allow you to use a
word processor, a spreadsheet, a mail client, and a print job all
running at the same time. Each processor (CPU) in
the system is probably only handling one thread (or job) at a
time, but it swaps between the jobs at such a rate that it gives
the illusion of running them all at the same time. It is easy to
create parallel threads of execution in an Erlang program and
to allow these threads to communicate with each other. In
Erlang, each thread of execution is called a <em>process</em>.</p><p>(Aside: the term "process" is usually used when the threads of
execution share no data with each other and the term "thread"
when they share data in some way. Threads of execution in Erlang
share no data, that is why they are called processes).</p><p>The Erlang BIF <strong>spawn</strong> is used to create a new process:
<strong>spawn(Module, Exported_Function, List of Arguments)</strong>.
Consider the following module:</p><pre><code class="">
-module(tut14).

-export([start/0, say_something/2]).

say_something(What, 0) -&gt;
    done;
say_something(What, Times) -&gt;
    io:format("~p~n", [What]),
    say_something(What, Times - 1).

start() -&gt;
    spawn(tut14, say_something, [hello, 3]),
    spawn(tut14, say_something, [goodbye, 3]).</code></pre><pre>
5&gt; <span class="input">c(tut14).</span>
{ok,tut14}
6&gt; <span class="input">tut14:say_something(hello, 3).</span>
hello
hello
hello
done</pre><p>As shown, the function <strong>say_something</strong> writes its first
argument the number of times specified by second argument.
The function <strong>start</strong> starts two Erlang processes,
one that writes "hello" three times and one that writes
"goodbye" three times. Both processes use the function
<strong>say_something</strong>. Notice that a function used in this way by
<strong>spawn</strong>, to start a process, must be exported from the module
(that is, in the <strong>-export</strong> at the start of the module).</p><pre>
9&gt; <span class="input">tut14:start().</span>
hello
goodbye
&lt;0.63.0&gt;
hello
goodbye
hello
goodbye</pre><p>Notice that it did not write "hello" three times and then
"goodbye" three times. Instead, the first process wrote a "hello",
the second a "goodbye", the first another "hello" and so forth.
But where did the &lt;0.63.0&gt; come from? The return value of a
function is the return value of the last "thing" in
the function. The last thing in the function <strong>start</strong> is</p><pre><code class="">
spawn(tut14, say_something, [goodbye, 3]).</code></pre><p><strong>spawn</strong> returns a <em>process identifier</em>, or
<em>pid</em>, which uniquely identifies the process. So &lt;0.63.0&gt;
is the pid of the <strong>spawn</strong> function call above.
The next example shows how to use pids.</p><p>Notice also that ~p is used instead of ~w in
<strong>io:format</strong>. To quote the manual: "~p Writes the data with
standard syntax in the same way as ~w, but breaks terms whose
printed representation is longer than one line into many lines
and indents each line sensibly. It also tries to detect lists of
printable characters and to output these as strings".</p><h3>Message Passing</h3><p>In the following example two processes are created and
they send messages to each other a number of times.</p><pre><code class="">
-module(tut15).

-export([start/0, ping/2, pong/0]).

ping(0, Pong_PID) -&gt;
    Pong_PID ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_PID) -&gt;
    Pong_PID ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_PID).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start() -&gt;
    Pong_PID = spawn(tut15, pong, []),
    spawn(tut15, ping, [3, Pong_PID]).</code></pre><pre>
1&gt; <span class="input">c(tut15).</span>
{ok,tut15}
2&gt; <span class="input">tut15: start().</span>
&lt;0.36.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished</pre><p>The function <strong>start</strong> first creates a process,
let us call it "pong":</p><pre><code class="">
Pong_PID = spawn(tut15, pong, [])</code></pre><p>This process executes <strong>tut15:pong()</strong>. <strong>Pong_PID</strong> is
the process identity of the "pong" process. The function
<strong>start</strong> now creates another process "ping":</p><pre><code class="">
spawn(tut15, ping, [3, Pong_PID]),</code></pre><p>This process executes:</p><pre><code class="">
tut15:ping(3, Pong_PID)</code></pre><p>&lt;0.36.0&gt; is the return value from the <strong>start</strong> function.</p><p>The process "pong" now does:</p><pre><code class="">
receive
    finished -&gt;
        io:format("Pong finished~n", []);
    {ping, Ping_PID} -&gt;
        io:format("Pong received ping~n", []),
        Ping_PID ! pong,
        pong()
end.</code></pre><p>The <strong>receive</strong> construct is used to allow processes to wait
for messages from other processes. It has the following format:</p><pre><code class="">
receive
   pattern1 -&gt;
       actions1;
   pattern2 -&gt;
       actions2;
   ....
   patternN
       actionsN
end.</code></pre><p>Notice there is no ";" before the <strong>end</strong>.</p><p>Messages between Erlang processes are simply valid Erlang terms.
That is, they can be lists, tuples, integers, atoms, pids,
and so on.</p><p>Each process has its own input queue for messages it receives.
New messages received are put at the end of the queue. When a
process executes a <strong>receive</strong>, the first message in the queue
is matched against the first pattern in the <strong>receive</strong>. If
this matches, the message is removed from the queue and
the actions corresponding to the pattern are executed.</p><p>However, if the first pattern does not match, the second pattern
is tested. If this matches, the message is removed from the queue
and the actions corresponding to the second pattern are executed.
If the second pattern does not match, the third is tried and so on
until there are no more patterns to test. If there are no more
patterns to test, the first message is kept in the queue and
the second message is tried instead. If this matches any pattern,
the appropriate actions are executed and the second message is
removed from the queue (keeping the first message and any other
messages in the queue). If the second message does not match,
the third message is tried, and so on, until the end of
the queue is reached. If the end of the queue is reached,
the process blocks
(stops execution) and waits until a new message is received and
this procedure is repeated.</p><p>The Erlang implementation is "clever" and minimizes
the number of times each message is tested against the patterns
in each <strong>receive</strong>.</p><p>Now back to the ping pong example.</p><p>"Pong" is waiting for messages. If the atom <strong>finished</strong> is
received, "pong" writes "Pong finished" to the output and, as it
has nothing more to do, terminates. If it receives a message with
the format:</p><pre><code class="">
{ping, Ping_PID}</code></pre><p>it writes "Pong received ping" to the output and sends the atom
<strong>pong</strong> to the process "ping":</p><pre><code class="">
Ping_PID ! pong</code></pre><p>Notice how the operator "!" is used to send messages. The syntax
of "!" is:</p><pre><code class="">
Pid ! Message</code></pre><p>That is, <strong>Message</strong> (any Erlang term) is sent to the process
with identity <strong>Pid</strong>.</p><p>After sending the message <strong>pong</strong> to the process "ping",
"pong" calls the <strong>pong</strong> function again, which causes it to
get back to the <strong>receive</strong> again and wait for another message.</p><p>Now let us look at the process "ping". Recall that it was started
by executing:</p><pre><code class="">
tut15:ping(3, Pong_PID)</code></pre><p>Looking at the function <strong>ping/2</strong>, the second
clause of <strong>ping/2</strong> is executed since the value of the first
argument is 3 (not 0) (first clause head is
<strong>ping(0,Pong_PID)</strong>, second clause head is
<strong>ping(N,Pong_PID)</strong>, so <strong>N</strong> becomes 3).</p><p>The second clause sends a message to "pong":</p><pre><code class="">
Pong_PID ! {ping, self()},</code></pre><p><strong>self()</strong> returns the pid of the process that executes
<strong>self()</strong>, in this case the pid of "ping". (Recall the code
for "pong", this lands up in the variable <strong>Ping_PID</strong> in
the <strong>receive</strong> previously explained.)</p><p>"Ping" now waits for a reply from "pong":</p><pre><code class="">
receive
    pong -&gt;
        io:format("Ping received pong~n", [])
end,</code></pre><p>It writes "Ping received pong" when this reply arrives, after
which "ping" calls the <strong>ping</strong> function again.</p><pre><code class="">
ping(N - 1, Pong_PID)</code></pre><p><strong>N-1</strong> causes the first argument to be decremented until it
becomes 0. When this occurs, the first clause of <strong>ping/2</strong>
is executed:</p><pre><code class="">
ping(0, Pong_PID) -&gt;
    Pong_PID !  finished,
    io:format("ping finished~n", []);</code></pre><p>The atom <strong>finished</strong> is sent to "pong" (causing it to
terminate as described above) and "ping finished" is written to
the output. "Ping" then terminates as it has nothing left
to do.</p><h3>Registered Process Names</h3><p>In the above example, "pong" was first created to be able
to give the identity of "pong" when "ping" was started. That is, in
some way "ping" must be able to know the identity of "pong" to be
able to send a message to it. Sometimes processes
which need to know each other's identities are started
independently of each other. Erlang thus provides a mechanism for
processes to be given names so that these names can be used as
identities instead of pids. This is done by using
the <strong>register</strong> BIF:</p><pre><code class="">
register(some_atom, Pid)</code></pre><p>Let us now rewrite the ping pong example using this and give
the name <strong>pong</strong> to the "pong" process:</p><pre><code class="">
-module(tut16).

-export([start/0, ping/1, pong/0]).

ping(0) -&gt;
    pong ! finished,
    io:format("ping finished~n", []);

ping(N) -&gt;
    pong ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start() -&gt;
    register(pong, spawn(tut16, pong, [])),
    spawn(tut16, ping, [3]).</code></pre><pre>
2&gt; <span class="input">c(tut16).</span>
{ok, tut16}
3&gt; <span class="input">tut16:start().</span>
&lt;0.38.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
ping finished
Pong finished</pre><p>Here the <strong>start/0</strong> function,</p><pre><code class="">
register(pong, spawn(tut16, pong, [])),</code></pre><p>both spawns the "pong" process and gives it the name <strong>pong</strong>.
In the "ping" process, messages can be sent to <strong>pong</strong> by:</p><pre><code class="">
pong ! {ping, self()},</code></pre><p><strong>ping/2</strong> now becomes <strong>ping/1</strong> as
the argument <strong>Pong_PID</strong> is not needed.</p><h3>Distributed Programming</h3><p>Let us rewrite the ping pong program with "ping" and "pong"
on different computers. First a few things
are needed to set up to get this to work. The distributed Erlang
implementation provides a very basic authentication mechanism to prevent
unintentional access to an Erlang system on another computer.
Erlang systems which talk to each other must have
the same <em>magic cookie</em>. The easiest way to achieve this
is by having a file called <strong>.erlang.cookie</strong> in your home
directory on all machines on which you are going to run
Erlang systems communicating with each other:
</p><ul><li>On Windows systems the home directory is the directory pointed out by the environment variable $HOME - you may need to set this.</li><li> On Linux or UNIX you can safely ignore this and simply create a file called <strong>.erlang.cookie</strong> in the directory you get to after executing the command <strong>cd</strong> without any argument.</li></ul><p>The <strong>.erlang.cookie</strong> file is to contain a line with
the same atom. For example, on Linux or UNIX, in the OS shell:</p><pre>
$ <span class="input">cd</span>
$ <span class="input">cat &gt; .erlang.cookie</span>
this_is_very_secret
$ <span class="input">chmod 400 .erlang.cookie</span></pre><p>The <strong>chmod</strong> above makes the <strong>.erlang.cookie</strong> file
accessible only by the owner of the file. This is a requirement.</p><p>When you start an Erlang system that is going to talk to other
Erlang systems, you must give it a name, for example:</p><pre>
$ <span class="input">erl -sname my_name</span></pre><p>We will see more details of this later. If you want to
experiment with distributed Erlang, but you only have one
computer to work on, you can start two separate Erlang systems on
the same computer but give them different names. Each Erlang
system running on a computer is called an <em>Erlang node</em>.</p><p>(Note: <strong>erl -sname</strong> assumes that all nodes are in the same
IP domain and we can use only the first component of the IP
address, if we want to use nodes in different domains we use
<strong>-name</strong> instead, but then all IP address must be given in
full.)</p><p>Here is the ping pong example modified to run on two separate
nodes:</p><pre><code class="">
-module(tut17).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    {pong, Pong_Node} ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start_pong() -&gt;
    register(pong, spawn(tut17, pong, [])).

start_ping(Pong_Node) -&gt;
    spawn(tut17, ping, [3, Pong_Node]).</code></pre><p>Let us assume there are two computers called gollum and kosken.
First a node is started on kosken, called ping, and then a node on gollum,
called pong.</p><p>On kosken (on a Linux/UNIX system):</p><pre>
kosken&gt; <span class="input">erl -sname ping</span>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(ping@kosken)1&gt;</pre><p>On gollum:</p><pre>
gollum&gt; <span class="input">erl -sname pong</span>
Erlang (BEAM) emulator version 5.2.3.7 [hipe] [threads:0]

Eshell V5.2.3.7  (abort with ^G)
(pong@gollum)1&gt;</pre><p>Now the "pong" process on gollum is started:</p><pre>
(pong@gollum)1&gt; <span class="input">tut17:start_pong().</span>
true</pre><p>And the "ping" process on kosken is started (from the code above you
can see that a parameter of the <strong>start_ping</strong> function is
the node name of the Erlang system where "pong" is running):</p><pre>
(ping@kosken)1&gt; <span class="input">tut17:start_ping(pong@gollum).</span>
&lt;0.37.0&gt;
Ping received pong
Ping received pong 
Ping received pong
ping finished</pre><p>As shown, the ping pong program has run. On the "pong" side:</p><pre>
(pong@gollum)2&gt;
Pong received ping                 
Pong received ping                 
Pong received ping                 
Pong finished                      
(pong@gollum)2&gt;</pre><p>Looking at the <strong>tut17</strong> code, you see that the <strong>pong</strong>
function itself is unchanged, the following lines work in the same way
irrespective of on which node the "ping" process is executes:</p><pre><code class="">
{ping, Ping_PID} -&gt;
    io:format("Pong received ping~n", []),
    Ping_PID ! pong,</code></pre><p>Thus, Erlang pids contain information about
where the process executes. So if you know the pid of a process,
the "!" operator can be used to send it a message disregarding
if the process is on the same node or on a different node.</p><p>A difference is how messages are sent to a registered process on
another node:</p><pre><code class="">
{pong, Pong_Node} ! {ping, self()},</code></pre><p>A tuple <strong>{registered_name,node_name}</strong> is used instead of
just the <strong>registered_name</strong>.</p><p>In the previous example, "ping" and "pong" were started from
the shells of two separate Erlang nodes. <strong>spawn</strong> can also be
used to start processes in other nodes.</p><p>The next example is the ping pong program, yet again,
but this time "ping" is started in another node:</p><pre><code class="">
-module(tut18).

-export([start/1,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    {pong, Pong_Node} ! finished,
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        finished -&gt;
            io:format("Pong finished~n", []);
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&gt;
    register(pong, spawn(tut18, pong, [])),
    spawn(Ping_Node, tut18, ping, [3, node()]).</code></pre><p>Assuming an Erlang system called ping (but not the "ping"
process) has already been started on kosken, then on gollum this is done:</p><pre>
(pong@gollum)1&gt; <span class="input">tut18:start(ping@kosken).</span>
&lt;3934.39.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong finished
ping finished</pre><p>Notice that all the output is received on gollum. This is because
the I/O
system finds out where the process is spawned from and sends all
output there.</p><h3>A Larger Example</h3><p>Now for a larger example with a simple
"messenger". The messenger is a program that allows users to log
in on different nodes and send simple messages to each other.</p><p>Before starting, notice the following:</p><ul><li> <p>This example only shows the message passing logic - no
attempt has been made to provide a nice graphical user
interface, although this can also be done in Erlang.</p> </li><li> <p>This sort of problem can be solved easier by use of
the facilities in OTP, which also provide methods for
updating code on the fly and so on (see
<a href="../doc/design_principles/des_princ#otp design principles"> OTP Design Principles</a>).</p> </li><li> <p>The first program contains some inadequacies
regarding handling of nodes which disappear.
These are corrected in a later version of the program.</p> </li></ul><p>The messenger is set up by allowing "clients" to connect to
a central server and say who and where they are. That is, a user
does not need to know the name of the Erlang node where another user
is located to send a message.</p><p>File <strong>messenger.erl</strong>:</p><a name="ex"></a><pre><code class="">
%%% Message passing utility.  
%%% User interface:
%%% logon(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at that node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as "messenger"
%%% Each node where there is a user logged on runs a client process registered
%%% as "mess_client" 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: {messenger, logged_off}
%%%
%%% To server: {ClientPid, logoff}
%%% Reply: no reply
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the "commands" and the client
%%% ----------------------------------------------
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/1, logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&gt;
    messenger@super.

%%% This is the server process for the "messenger"
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server(User_List) -&gt;
    receive
        {From, logon, Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {From, logoff} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&gt;
            server_transfer(From, To, Message, User_List),
            io:format("list is now: ~p~n", [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(messenger, server, [[]])).


%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&gt;
            From ! {messenger, logged_on},
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {From, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.
%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.


%%% User Commands
logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! {message_to, ToName, Message},
             ok
end.


%%% The client process which runs on each server node
client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            {messenger, Server_Node} ! {self(), logoff},
            exit(normal);
        {message_to, ToName, Message} -&gt;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        {messenger, stop, Why} -&gt; % Stop the client 
            io:format("~p~n", [Why]),
            exit(normal);
        {messenger, What} -&gt;  % Normal response
            io:format("~p~n", [What])
    end.</code></pre><p>To use this program, you need to:</p><ul><li>Configure the <strong>server_node()</strong> function.</li><li>Copy the compiled code (<strong>messenger.beam</strong>) to the directory on each computer where you start Erlang.</li></ul><p>In the following example using this program,
nodes are started on four different computers. If you do not have that
many machines available on your network, you can start
several nodes on the same machine.</p><p>Four Erlang nodes are started up: messenger@super, c1@bilbo,
c2@kosken, c3@gollum.</p><p>First the server at messenger@super is started up:</p><pre>
(messenger@super)1&gt; <span class="input">messenger:start_server().</span>
true</pre><p>Now Peter logs on at c1@bilbo:</p><pre>
(c1@bilbo)1&gt; <span class="input">messenger:logon(peter).</span>
true
logged_on</pre><p>James logs on at c2@kosken:</p><pre>
(c2@kosken)1&gt; <span class="input">messenger:logon(james).</span>
true
logged_on</pre><p>And Fred logs on at c3@gollum:</p><pre>
(c3@gollum)1&gt; <span class="input">messenger:logon(fred).</span>
true
logged_on</pre><p>Now Peter sends Fred a message:</p><pre>
(c1@bilbo)2&gt; <span class="input">messenger:message(fred, "hello").</span>
ok
sent</pre><p>Fred receives the message and sends a message to Peter and
logs off:</p><pre>
Message from peter: "hello"
(c3@gollum)2&gt; <span class="input">messenger:message(peter, "go away, I'm busy").</span>
ok
sent
(c3@gollum)3&gt; <span class="input">messenger:logoff().</span>
logoff</pre><p>James now tries to send a message to Fred:</p><pre>
(c2@kosken)2&gt; <span class="input">messenger:message(fred, "peter doesn't like you").</span>
ok
receiver_not_found</pre><p>But this fails as Fred has already logged off.</p><p>First let us look at some of the new concepts that have
been introduced.</p><p>There are two versions of the <strong>server_transfer</strong> function:
one with four arguments (<strong>server_transfer/4</strong>) and one with
five (<strong>server_transfer/5</strong>). These are regarded by Erlang as
two separate functions.</p><p>Notice how to write the <strong>server</strong> function so that it calls
itself, through <strong>server(User_List)</strong>, and thus creates a loop.
The Erlang compiler is "clever" and optimizes the code so that
this really is a sort of loop and not a proper function call. But
this only works if there is no code after the call. Otherwise,
the compiler expects the call to return and make a proper
function call. This would result in the process getting bigger
and bigger for every loop.</p><p>Functions in the <strong>lists</strong> module are used. This is a very
useful module and a study of the manual page is recommended
(<strong>erl -man lists</strong>).
<strong>lists:keymember(Key,Position,Lists)</strong> looks through a list
of tuples and looks at <strong>Position</strong> in each tuple to see if it
is the same as <strong>Key</strong>. The first element is position 1. If it
finds a tuple where the element at <strong>Position</strong> is the same as
<strong>Key</strong>, it returns <strong>true</strong>, otherwise <strong>false</strong>.</p><pre>
3&gt; <span class="input">lists:keymember(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</span>
true
4&gt; <span class="input">lists:keymember(p, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</span>
false</pre><p><strong>lists:keydelete</strong> works in the same way but deletes
the first tuple found (if any) and returns the remaining list:</p><pre>
5&gt; <span class="input">lists:keydelete(a, 2, [{x,y,z},{b,b,b},{b,a,c},{q,r,s}]).</span>
[{x,y,z},{b,b,b},{q,r,s}]</pre><p><strong>lists:keysearch</strong> is like <strong>lists:keymember</strong>, but it
returns <strong>{value,Tuple_Found}</strong> or the atom <strong>false</strong>.</p><p>There are many very useful functions in the <strong>lists</strong>
module.</p><p>An Erlang process (conceptually) runs until it does a
<strong>receive</strong> and there is no message which it wants to receive
in the message queue. "conceptually" is used here because the Erlang
system shares the CPU time between the active processes in
the system.</p><p>A process terminates when there is nothing more for it to do,
that is, the last function it calls simply returns and does not call
another function. Another way for a process to terminate is for
it to call <strong>exit/1</strong>. The argument to <strong>exit/1</strong> has a
special meaning, which is discussed later. In this example,
<strong>exit(normal)</strong> is done, which has the same effect as a
process running out of functions to call.</p><p>The BIF <strong>whereis(RegisteredName)</strong> checks if a registered
process of name <strong>RegisteredName</strong> exists. If it exists, the pid of
that process is returned. If it does not exist, the atom
<strong>undefined</strong> is returned.</p><p>You should by now be able to understand most of the code in the
messenger-module. Let us study one case in detail: a message is
sent from one user to another.</p><p>The first user "sends" the message in the example above by:</p><pre><code class="">
messenger:message(fred, "hello")</code></pre><p>After testing that the client process exists:</p><pre><code class="">
whereis(mess_client) </code></pre><p>And a message is sent to <strong>mess_client</strong>:</p><pre><code class="">
mess_client ! {message_to, fred, "hello"}</code></pre><p>The client sends the message to the server by:</p><pre><code class="">
{messenger, messenger@super} ! {self(), message_to, fred, "hello"},</code></pre><p>And waits for a reply from the server.</p><p>The server receives this message and calls:</p><pre><code class="">
server_transfer(From, fred, "hello", User_List),</code></pre><p>This checks that the pid <strong>From</strong> is in the <strong>User_List</strong>:</p><pre><code class="">
lists:keysearch(From, 1, User_List) </code></pre><p>If <strong>keysearch</strong> returns the atom <strong>false</strong>, some
error has occurred and the server sends back the message:</p><pre><code class="">
From ! {messenger, stop, you_are_not_logged_on}</code></pre><p>This is received by the client, which in turn does
<strong>exit(normal)</strong> and terminates. If <strong>keysearch</strong> returns
<strong>{value,{From,Name}}</strong> it is certain that the user is logged on and
that his name (peter) is in variable <strong>Name</strong>.</p><p>Let us now call:</p><pre><code class="">
server_transfer(From, peter, fred, "hello", User_List)</code></pre><p>Notice that as this is <strong>server_transfer/5</strong>, it is not the same
as the previous function <strong>server_transfer/4</strong>. Another
<strong>keysearch</strong> is done on <strong>User_List</strong> to find the pid of
the client corresponding to fred:</p><pre><code class="">
lists:keysearch(fred, 2, User_List)</code></pre><p>This time argument 2 is used, which is the second element in
the tuple. If this returns the atom <strong>false</strong>,
fred is not logged on and the following message is sent:</p><pre><code class="">
From ! {messenger, receiver_not_found};</code></pre><p>This is received by the client.</p><p> If <strong>keysearch</strong> returns:</p><pre><code class="">
{value, {ToPid, fred}}</code></pre><p>The following message is sent to fred's client:</p><pre><code class="">
ToPid ! {message_from, peter, "hello"}, </code></pre><p>The following message is sent to peter's client:</p><pre><code class="">
From ! {messenger, sent} </code></pre><p>Fred's client receives the message and prints it:</p><pre><code class="">
{message_from, peter, "hello"} -&gt;
    io:format("Message from ~p: ~p~n", [peter, "hello"])</code></pre><p>Peter's client receives the message in
the <strong>await_result</strong> function.</p><p>Several things are wrong with the messenger example in
<a href="./conc_prog#ex">A Larger Example</a>.
For example, if a node where a user is logged
on goes down without doing a logoff, the user remains in
the server's <strong>User_List</strong>, but the client disappears. This
makes it impossible for the user to log on again as the server
thinks the user already is logged on.</p><p>Or what happens if the server goes down in the middle of sending a
message, leaving the sending client hanging forever in
the <strong>await_result</strong> function?</p><h3>Time-outs</h3><p>Before improving the messenger program, let us look at some
general principles, using the ping pong program as an example.
Recall that when "ping" finishes, it tells "pong" that it has
done so by sending the atom <strong>finished</strong> as a message to "pong"
so that "pong" can also finish. Another way to let "pong"
finish is to make "pong" exit if it does not receive a message
from ping within a certain time. This can be done by adding a
<em>time-out</em> to <strong>pong</strong> as shown in the following example:</p><pre><code class="">
-module(tut19).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    after 5000 -&gt;
            io:format("Pong timed out~n", [])
    end.

start_pong() -&gt;
    register(pong, spawn(tut19, pong, [])).

start_ping(Pong_Node) -&gt;
    spawn(tut19, ping, [3, Pong_Node]).</code></pre><p>After this is compiled and the file <strong>tut19.beam</strong>
is copied to the necessary directories, the following is seen
on (pong@kosken): </p><pre>
(pong@kosken)1&gt; <span class="input">tut19:start_pong().</span>
true
Pong received ping
Pong received ping
Pong received ping
Pong timed out</pre><p>And the following is seen on (ping@gollum):</p><pre>
(ping@gollum)1&gt; <span class="input">tut19:start_ping(pong@kosken).</span>
&lt;0.36.0&gt;
Ping received pong
Ping received pong
Ping received pong
ping finished   </pre><p>The time-out is set in:</p><pre><code class="">
pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    after 5000 -&gt;
            io:format("Pong timed out~n", [])
    end.</code></pre><p>The time-out (<strong>after 5000</strong>) is started when
<strong>receive</strong> is entered.
The time-out is canceled if <strong>{ping,Ping_PID}</strong>
is received. If <strong>{ping,Ping_PID}</strong> is not received,
the actions following the time-out are done after 5000
milliseconds. <strong>after</strong> must be last in the <strong>receive</strong>,
that is, preceded by all other message reception specifications in
the <strong>receive</strong>. It is also possible to call a function that
returned an integer for the time-out:</p><pre><code class="">
after pong_timeout() -&gt;</code></pre><p>In general, there are better ways than using time-outs to
supervise parts of a distributed Erlang system. Time-outs are
usually appropriate to supervise external events, for example, if
you have expected a message from some external system within a
specified time. For example, a time-out can be used to log a user
out of the messenger system if they have not accessed it for,
say, ten minutes.</p><h3>Error Handling</h3><p>Before going into details of the supervision and error handling
in an Erlang system, let us see how Erlang processes terminate,
or in Erlang terminology, <em>exit</em>.</p><p>A process which executes <strong>exit(normal)</strong> or simply runs out
of things to do has a <em>normal</em> exit.</p><p>A process which encounters a runtime error (for example, divide by zero,
bad match, trying to call a function that does not exist and so on)
exits with an error, that is, has an <em>abnormal</em> exit. A
process which executes
<a href="../erts/erlang#exit/1">exit(Reason)</a>
where <strong>Reason</strong> is any Erlang term except the atom
<strong>normal</strong>, also has an abnormal exit.</p><p>An Erlang process can set up links to other Erlang processes. If
a process calls
<a href="../erts/erlang#link/1">link(Other_Pid)</a>
it sets up a bidirectional link between itself and the process
called <strong>Other_Pid</strong>. When a process terminates, it sends
something called a <em>signal</em> to all the processes it has
links to.</p><p>The signal carries information about the pid it was sent from and
the exit reason.</p><p>The default behaviour of a process that receives a normal exit
is to ignore the signal.</p><p>The default behaviour in the two other cases (that is, abnormal exit)
above is to:</p><ul><li>Bypass all messages to the receiving process.</li><li>Kill the receiving process.</li><li>Propagate the same error signal to the links of the killed process.</li></ul><p>In this way you can connect all processes in a
transaction together using links. If one of the processes
exits abnormally, all the processes in the transaction are
killed. As it is often wanted to create a process and link to it at
the same time, there is a special BIF,
<a href="../erts/erlang#spawn_link/1">spawn_link</a>
that does the same as <strong>spawn</strong>, but also creates a link to
the spawned process.</p><p>Now an example of the ping pong example using links to terminate
"pong":</p><pre><code class="">
-module(tut20).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&gt;
    link(Pong_Pid),
    ping1(N, Pong_Pid).

ping1(0, _) -&gt;
    exit(ping);

ping1(N, Pong_Pid) -&gt;
    Pong_Pid ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&gt;
    PongPID = spawn(tut20, pong, []),
    spawn(Ping_Node, tut20, ping, [3, PongPID]).</code></pre><pre>
(s1@bill)3&gt; <span class="input">tut20:start(s2@kosken).</span>
Pong received ping
&lt;3820.41.0&gt;
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong</pre><p>This is a slight modification of the ping pong program where both
processes are spawned from the same <strong>start/1</strong> function,
and the "ping" process can be spawned on a separate node. Notice
the use of the <strong>link</strong> BIF. "Ping" calls
<strong>exit(ping)</strong> when it finishes and this causes an exit
signal to be sent to "pong", which also terminates.</p><p>It is possible to modify the default behaviour of a process so
that it does not get killed when it receives abnormal exit
signals. Instead, all signals are turned into normal messages on
the format <strong>{'EXIT',FromPID,Reason}</strong> and added to the end of
the receiving process' message queue. This behaviour is set by:</p><pre><code class="">
process_flag(trap_exit, true)</code></pre><p>There are several other process flags, see
<a href="../erts/erlang#process_flag/2">erlang(3)</a>.
Changing the default behaviour of a process in this way is
usually not done in standard user programs, but is left to
the supervisory programs in OTP.
However, the ping pong program is modified to illustrate exit
trapping.</p><pre><code class="">
-module(tut21).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&gt;
    link(Pong_Pid), 
    ping1(N, Pong_Pid).

ping1(0, _) -&gt;
    exit(ping);

ping1(N, Pong_Pid) -&gt;
    Pong_Pid ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&gt;
    process_flag(trap_exit, true), 
    pong1().

pong1() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong1();
        {'EXIT', From, Reason} -&gt;
            io:format("pong exiting, got ~p~n", [{'EXIT', From, Reason}])
    end.

start(Ping_Node) -&gt;
    PongPID = spawn(tut21, pong, []),
    spawn(Ping_Node, tut21, ping, [3, PongPID]).</code></pre><pre>
(s1@bill)1&gt; <span class="input">tut21:start(s2@gollum).</span>
&lt;3820.39.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
pong exiting, got {'EXIT',&lt;3820.39.0&gt;,ping}</pre><h3>The Larger Example with Robustness Added</h3><p>Let us return to the messenger program and add changes to
make it more robust:</p><pre><code class="">
%%% Message passing utility.  
%%% User interface:
%%% login(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at that node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as "messenger"
%%% Each node where there is a user logged on runs a client process registered
%%% as "mess_client" 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% When the client terminates for some reason
%%% To server: {'EXIT', ClientPid, Reason}
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the "commands" and the client
%%% ---------------------------------------------- 
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/0, 
         logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&gt;
    messenger@super.

%%% This is the server process for the "messenger"
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server() -&gt;
    process_flag(trap_exit, true),
    server([]).

server(User_List) -&gt;
    receive
        {From, logon, Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {'EXIT', From, _} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&gt;
            server_transfer(From, To, Message, User_List),
            io:format("list is now: ~p~n", [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(messenger, server, [])).

%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&gt;
            From ! {messenger, logged_on},
            link(From),
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {_, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.

%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.

%%% User Commands
logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! {message_to, ToName, Message},
             ok
end.

%%% The client process which runs on each user node
client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            exit(normal);
        {message_to, ToName, Message} -&gt;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        {messenger, stop, Why} -&gt; % Stop the client 
            io:format("~p~n", [Why]),
            exit(normal);
        {messenger, What} -&gt;  % Normal response
            io:format("~p~n", [What])
    after 5000 -&gt;
            io:format("No response from server~n", []),
            exit(timeout)
    end.</code></pre><p>The following changes are added:</p><p>The messenger server traps exits. If it receives an exit signal,
<strong>{'EXIT',From,Reason}</strong>, this means that a client process has
terminated or is unreachable for one of the following reasons:</p><ul><li>The user has logged off (the "logoff" message is removed).</li><li>The network connection to the client is broken.</li><li>The node on which the client process resides has gone down.</li><li>The client processes has done some illegal operation.</li></ul><p>If an exit signal is received as above, the tuple
<strong>{From,Name}</strong> is deleted from the servers <strong>User_List</strong> using
the <strong>server_logoff</strong> function. If the node on which the server
runs goes down, an exit signal (automatically generated by
the system) is sent to all of the client processes:
<strong>{'EXIT',MessengerPID,noconnection}</strong> causing all the client
processes to terminate.</p><p>Also, a time-out of five seconds has been introduced in
the <strong>await_result</strong> function. That is, if the server does not
reply within five seconds (5000 ms), the client terminates. This
is only needed in the logon sequence before the client and the
server are linked.</p><p>An interesting case is if the client terminates before
the server links to it. This is taken care of because linking to a
non-existent process causes an exit signal,
<strong>{'EXIT',From,noproc}</strong>, to be automatically generated. This is
as if the process terminated immediately after the link operation.</p><p>Larger programs are usually written as a collection of files with
a well-defined interface between the various parts.</p><h3>The Larger Example Divided into Several Files</h3><p>To illustrate this, the messenger example from
the previous section is divided into the following five files:</p><ul><li> <p><strong>mess_config.hrl</strong></p> <p>Header file for configuration data</p></li><li> <p><strong>mess_interface.hrl</strong></p> <p>Interface definitions between the client and the messenger</p></li><li> <p><strong>user_interface.erl</strong></p> <p>Functions for the user interface</p></li><li> <p><strong>mess_client.erl</strong></p> <p>Functions for the client side of the messenger</p></li><li> <p><strong>mess_server.erl</strong></p> <p>Functions for the server side of the messenger</p></li></ul><p>While doing this, the message passing interface between the shell,
the client, and the server is cleaned up and is defined
using <em>records</em>. Also, <em>macros</em> are introduced:</p><pre><code class="">
%%%----FILE mess_config.hrl----

%%% Configure the location of the server node,
-define(server_node, messenger@super).

%%%----END FILE----</code></pre><pre><code class="">
%%%----FILE mess_interface.hrl----

%%% Message interface between client and server and client shell for
%%% messenger program 

%%%Messages from Client to server received in server/1 function.
-record(logon,{client_pid, username}).
-record(message,{client_pid, to_name, message}).
%%% {'EXIT', ClientPid, Reason}  (client terminated or unreachable.

%%% Messages from Server to Client, received in await_result/0 function 
-record(abort_client,{message}).
%%% Messages are: user_exists_at_other_node, 
%%%               you_are_not_logged_on
-record(server_reply,{message}).
%%% Messages are: logged_on
%%%               receiver_not_found
%%%               sent  (Message has been sent (no guarantee)
%%% Messages from Server to Client received in client/1 function
-record(message_from,{from_name, message}).

%%% Messages from shell to Client received in client/1 function
%%% spawn(mess_client, client, [server_node(), Name])
-record(message_to,{to_name, message}).
%%% logoff

%%%----END FILE----</code></pre><pre><code class="">
%%%----FILE user_interface.erl----

%%% User interface to the messenger program
%%% login(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.

%%% logoff()
%%%     Logs off anybody at that node

%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.

-module(user_interface).
-export([logon/1, logoff/0, message/2]).
-include("mess_interface.hrl").
-include("mess_config.hrl").

logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(mess_client, client, [?server_node, Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! #message_to{to_name=ToName, message=Message},
             ok
end.

%%%----END FILE----</code></pre><pre><code class="">
%%%----FILE mess_client.erl----

%%% The client process which runs on each user node

-module(mess_client).
-export([client/2]).
-include("mess_interface.hrl").

client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! #logon{client_pid=self(), username=Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            exit(normal);
        #message_to{to_name=ToName, message=Message} -&gt;
            {messenger, Server_Node} ! 
                #message{client_pid=self(), to_name=ToName, message=Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        #abort_client{message=Why} -&gt;
            io:format("~p~n", [Why]),
            exit(normal);
        #server_reply{message=What} -&gt;
            io:format("~p~n", [What])
    after 5000 -&gt;
            io:format("No response from server~n", []),
            exit(timeout)
    end.

%%%----END FILE---</code></pre><pre><code class="">
%%%----FILE mess_server.erl----

%%% This is the server process of the messenger service

-module(mess_server).
-export([start_server/0, server/0]).
-include("mess_interface.hrl").

server() -&gt;
    process_flag(trap_exit, true),
    server([]).

%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server(User_List) -&gt;
    io:format("User list = ~p~n", [User_List]),
    receive
        #logon{client_pid=From, username=Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {'EXIT', From, _} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        #message{client_pid=From, to_name=To, message=Message} -&gt;
            server_transfer(From, To, Message, User_List),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(?MODULE, server, [])).

%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! #abort_client{message=user_exists_at_other_node},
            User_List;
        false -&gt;
            From ! #server_reply{message=logged_on},
            link(From),
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).

%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! #abort_client{message=you_are_not_logged_on};
        {value, {_, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.
%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! #server_reply{message=receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! #message_from{from_name=Name, message=Message}, 
            From !  #server_reply{message=sent} 
    end.

%%%----END FILE---</code></pre><h3>Header Files</h3><p>As shown above, some files have extension <strong>.hrl</strong>. These
are header files that are included in the <strong>.erl</strong> files by:</p><pre><code class="">
-include("File_Name").</code></pre><p>for example:</p><pre><code class="">
-include("mess_interface.hrl").</code></pre><p>In the case above the file is fetched from the same directory as
all the other files in the messenger example. (*manual*).</p><p>.hrl files can contain any valid Erlang code but are most often
used for record and macro definitions.</p><h3>Records</h3><p>A record is defined as:</p><pre><code class="">
-record(name_of_record,{field_name1, field_name2, field_name3, ......}).</code></pre><p>For example:</p><pre><code class="">
-record(message_to,{to_name, message}).</code></pre><p>This is equivalent to:</p><pre><code class="">
{message_to, To_Name, Message}</code></pre><p>Creating a record is best illustrated by an example:</p><pre><code class="">
#message_to{message="hello", to_name=fred)</code></pre><p>This creates:</p><pre><code class="">
{message_to, fred, "hello"}</code></pre><p>Notice that you do not have to worry about the order you assign
values to the various parts of the records when you create it.
The advantage of using records is that by placing their
definitions in header files you can conveniently define
interfaces that are easy to change. For example, if you want to
add a new field to the record, you only have to change
the code where the new field is used and not at every place
the record is referred to. If you leave out a field when creating
a record, it gets the value of the atom <strong>undefined</strong>. (*manual*)</p><p>Pattern matching with records is very similar to creating
records. For example, inside a <strong>case</strong> or <strong>receive</strong>:</p><pre><code class="">
#message_to{to_name=ToName, message=Message} -&gt;</code></pre><p>This is the same as:</p><pre><code class="">
{message_to, ToName, Message}</code></pre><h3>Macros</h3><p>Another thing that has been added to the messenger is a macro.
The file <strong>mess_config.hrl</strong> contains the definition:</p><pre><code class="">
%%% Configure the location of the server node,
-define(server_node, messenger@super).</code></pre><p>This file is included in <strong>mess_server.erl</strong>:</p><pre><code class="">
-include("mess_config.hrl").</code></pre><p>Every occurrence of <strong>?server_node</strong> in <strong>mess_server.erl</strong>
is now replaced by <strong>messenger@super</strong>.</p><p>A macro is also used when spawning the server process:</p><pre><code class="">
spawn(?MODULE, server, [])</code></pre><p>This is a standard macro (that is, defined by the system, not by
the user). <strong>?MODULE</strong> is always replaced by the name of the
current module (that is, the <strong>-module</strong> definition near the start
of the file). There are more advanced ways of using macros with,
for example, parameters (*manual*).</p><p>The three Erlang (<strong>.erl</strong>) files in the messenger example are
individually compiled into object code file (<strong>.beam</strong>).
The Erlang system loads and links these files into the system
when they are referred to during execution of the code. In this
case, they are simply put in our current working directory
(that is, the place you have done "cd" to).
There are ways of putting the <strong>.beam</strong> files in other
directories.</p><p>In the messenger example, no assumptions have been made about
what the message being sent is. It can be any valid Erlang term.</p></body></html>