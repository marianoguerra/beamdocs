<!doctype html>
<html><head><meta charset="utf-8"><link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.0/css/bootstrap.min.css"></head><body style="margin: 4em 10%"><h1>Robustness</h1><p>Several things are wrong with the messenger example in
<a href="./conc_prog#ex">A Larger Example</a>.
For example, if a node where a user is logged
on goes down without doing a logoff, the user remains in
the server's <strong>User_List</strong>, but the client disappears. This
makes it impossible for the user to log on again as the server
thinks the user already is logged on.</p><p>Or what happens if the server goes down in the middle of sending a
message, leaving the sending client hanging forever in
the <strong>await_result</strong> function?</p><h2>Time-outs</h2><p>Before improving the messenger program, let us look at some
general principles, using the ping pong program as an example.
Recall that when "ping" finishes, it tells "pong" that it has
done so by sending the atom <strong>finished</strong> as a message to "pong"
so that "pong" can also finish. Another way to let "pong"
finish is to make "pong" exit if it does not receive a message
from ping within a certain time. This can be done by adding a
<em>time-out</em> to <strong>pong</strong> as shown in the following example:</p><pre><code class="">
-module(tut19).

-export([start_ping/1, start_pong/0,  ping/2, pong/0]).

ping(0, Pong_Node) -&gt;
    io:format("ping finished~n", []);

ping(N, Pong_Node) -&gt;
    {pong, Pong_Node} ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping(N - 1, Pong_Node).

pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    after 5000 -&gt;
            io:format("Pong timed out~n", [])
    end.

start_pong() -&gt;
    register(pong, spawn(tut19, pong, [])).

start_ping(Pong_Node) -&gt;
    spawn(tut19, ping, [3, Pong_Node]).</code></pre><p>After this is compiled and the file <strong>tut19.beam</strong>
is copied to the necessary directories, the following is seen
on (pong@kosken): </p><pre>
(pong@kosken)1&gt; <span class="input">tut19:start_pong().</span>
true
Pong received ping
Pong received ping
Pong received ping
Pong timed out</pre><p>And the following is seen on (ping@gollum):</p><pre>
(ping@gollum)1&gt; <span class="input">tut19:start_ping(pong@kosken).</span>
&lt;0.36.0&gt;
Ping received pong
Ping received pong
Ping received pong
ping finished   </pre><p>The time-out is set in:</p><pre><code class="">
pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    after 5000 -&gt;
            io:format("Pong timed out~n", [])
    end.</code></pre><p>The time-out (<strong>after 5000</strong>) is started when
<strong>receive</strong> is entered.
The time-out is canceled if <strong>{ping,Ping_PID}</strong>
is received. If <strong>{ping,Ping_PID}</strong> is not received,
the actions following the time-out are done after 5000
milliseconds. <strong>after</strong> must be last in the <strong>receive</strong>,
that is, preceded by all other message reception specifications in
the <strong>receive</strong>. It is also possible to call a function that
returned an integer for the time-out:</p><pre><code class="">
after pong_timeout() -&gt;</code></pre><p>In general, there are better ways than using time-outs to
supervise parts of a distributed Erlang system. Time-outs are
usually appropriate to supervise external events, for example, if
you have expected a message from some external system within a
specified time. For example, a time-out can be used to log a user
out of the messenger system if they have not accessed it for,
say, ten minutes.</p><h2>Error Handling</h2><p>Before going into details of the supervision and error handling
in an Erlang system, let us see how Erlang processes terminate,
or in Erlang terminology, <em>exit</em>.</p><p>A process which executes <strong>exit(normal)</strong> or simply runs out
of things to do has a <em>normal</em> exit.</p><p>A process which encounters a runtime error (for example, divide by zero,
bad match, trying to call a function that does not exist and so on)
exits with an error, that is, has an <em>abnormal</em> exit. A
process which executes
<a href="../erts/erlang#exit/1">exit(Reason)</a>
where <strong>Reason</strong> is any Erlang term except the atom
<strong>normal</strong>, also has an abnormal exit.</p><p>An Erlang process can set up links to other Erlang processes. If
a process calls
<a href="../erts/erlang#link/1">link(Other_Pid)</a>
it sets up a bidirectional link between itself and the process
called <strong>Other_Pid</strong>. When a process terminates, it sends
something called a <em>signal</em> to all the processes it has
links to.</p><p>The signal carries information about the pid it was sent from and
the exit reason.</p><p>The default behaviour of a process that receives a normal exit
is to ignore the signal.</p><p>The default behaviour in the two other cases (that is, abnormal exit)
above is to:</p><ul><li>Bypass all messages to the receiving process.</li><li>Kill the receiving process.</li><li>Propagate the same error signal to the links of the killed process.</li></ul><p>In this way you can connect all processes in a
transaction together using links. If one of the processes
exits abnormally, all the processes in the transaction are
killed. As it is often wanted to create a process and link to it at
the same time, there is a special BIF,
<a href="../erts/erlang#spawn_link/1">spawn_link</a>
that does the same as <strong>spawn</strong>, but also creates a link to
the spawned process.</p><p>Now an example of the ping pong example using links to terminate
"pong":</p><pre><code class="">
-module(tut20).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&gt;
    link(Pong_Pid),
    ping1(N, Pong_Pid).

ping1(0, _) -&gt;
    exit(ping);

ping1(N, Pong_Pid) -&gt;
    Pong_Pid ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong()
    end.

start(Ping_Node) -&gt;
    PongPID = spawn(tut20, pong, []),
    spawn(Ping_Node, tut20, ping, [3, PongPID]).</code></pre><pre>
(s1@bill)3&gt; <span class="input">tut20:start(s2@kosken).</span>
Pong received ping
&lt;3820.41.0&gt;
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong</pre><p>This is a slight modification of the ping pong program where both
processes are spawned from the same <strong>start/1</strong> function,
and the "ping" process can be spawned on a separate node. Notice
the use of the <strong>link</strong> BIF. "Ping" calls
<strong>exit(ping)</strong> when it finishes and this causes an exit
signal to be sent to "pong", which also terminates.</p><p>It is possible to modify the default behaviour of a process so
that it does not get killed when it receives abnormal exit
signals. Instead, all signals are turned into normal messages on
the format <strong>{'EXIT',FromPID,Reason}</strong> and added to the end of
the receiving process' message queue. This behaviour is set by:</p><pre><code class="">
process_flag(trap_exit, true)</code></pre><p>There are several other process flags, see
<a href="../erts/erlang#process_flag/2">erlang(3)</a>.
Changing the default behaviour of a process in this way is
usually not done in standard user programs, but is left to
the supervisory programs in OTP.
However, the ping pong program is modified to illustrate exit
trapping.</p><pre><code class="">
-module(tut21).

-export([start/1,  ping/2, pong/0]).

ping(N, Pong_Pid) -&gt;
    link(Pong_Pid), 
    ping1(N, Pong_Pid).

ping1(0, _) -&gt;
    exit(ping);

ping1(N, Pong_Pid) -&gt;
    Pong_Pid ! {ping, self()},
    receive
        pong -&gt;
            io:format("Ping received pong~n", [])
    end,
    ping1(N - 1, Pong_Pid).

pong() -&gt;
    process_flag(trap_exit, true), 
    pong1().

pong1() -&gt;
    receive
        {ping, Ping_PID} -&gt;
            io:format("Pong received ping~n", []),
            Ping_PID ! pong,
            pong1();
        {'EXIT', From, Reason} -&gt;
            io:format("pong exiting, got ~p~n", [{'EXIT', From, Reason}])
    end.

start(Ping_Node) -&gt;
    PongPID = spawn(tut21, pong, []),
    spawn(Ping_Node, tut21, ping, [3, PongPID]).</code></pre><pre>
(s1@bill)1&gt; <span class="input">tut21:start(s2@gollum).</span>
&lt;3820.39.0&gt;
Pong received ping
Ping received pong
Pong received ping
Ping received pong
Pong received ping
Ping received pong
pong exiting, got {'EXIT',&lt;3820.39.0&gt;,ping}</pre><h2>The Larger Example with Robustness Added</h2><p>Let us return to the messenger program and add changes to
make it more robust:</p><pre><code class="">
%%% Message passing utility.  
%%% User interface:
%%% login(Name)
%%%     One user at a time can log in from each Erlang node in the
%%%     system messenger: and choose a suitable Name. If the Name
%%%     is already logged in at another node or if someone else is
%%%     already logged in at the same node, login will be rejected
%%%     with a suitable error message.
%%% logoff()
%%%     Logs off anybody at that node
%%% message(ToName, Message)
%%%     sends Message to ToName. Error messages if the user of this 
%%%     function is not logged on or if ToName is not logged on at
%%%     any node.
%%%
%%% One node in the network of Erlang nodes runs a server which maintains
%%% data about the logged on users. The server is registered as "messenger"
%%% Each node where there is a user logged on runs a client process registered
%%% as "mess_client" 
%%%
%%% Protocol between the client processes and the server
%%% ----------------------------------------------------
%%% 
%%% To server: {ClientPid, logon, UserName}
%%% Reply {messenger, stop, user_exists_at_other_node} stops the client
%%% Reply {messenger, logged_on} logon was successful
%%%
%%% When the client terminates for some reason
%%% To server: {'EXIT', ClientPid, Reason}
%%%
%%% To server: {ClientPid, message_to, ToName, Message} send a message
%%% Reply: {messenger, stop, you_are_not_logged_on} stops the client
%%% Reply: {messenger, receiver_not_found} no user with this name logged on
%%% Reply: {messenger, sent} Message has been sent (but no guarantee)
%%%
%%% To client: {message_from, Name, Message},
%%%
%%% Protocol between the "commands" and the client
%%% ---------------------------------------------- 
%%%
%%% Started: messenger:client(Server_Node, Name)
%%% To client: logoff
%%% To client: {message_to, ToName, Message}
%%%
%%% Configuration: change the server_node() function to return the
%%% name of the node where the messenger server runs

-module(messenger).
-export([start_server/0, server/0, 
         logon/1, logoff/0, message/2, client/2]).

%%% Change the function below to return the name of the node where the
%%% messenger server runs
server_node() -&gt;
    messenger@super.

%%% This is the server process for the "messenger"
%%% the user list has the format [{ClientPid1, Name1},{ClientPid22, Name2},...]
server() -&gt;
    process_flag(trap_exit, true),
    server([]).

server(User_List) -&gt;
    receive
        {From, logon, Name} -&gt;
            New_User_List = server_logon(From, Name, User_List),
            server(New_User_List);
        {'EXIT', From, _} -&gt;
            New_User_List = server_logoff(From, User_List),
            server(New_User_List);
        {From, message_to, To, Message} -&gt;
            server_transfer(From, To, Message, User_List),
            io:format("list is now: ~p~n", [User_List]),
            server(User_List)
    end.

%%% Start the server
start_server() -&gt;
    register(messenger, spawn(messenger, server, [])).

%%% Server adds a new user to the user list
server_logon(From, Name, User_List) -&gt;
    %% check if logged on anywhere else
    case lists:keymember(Name, 2, User_List) of
        true -&gt;
            From ! {messenger, stop, user_exists_at_other_node},  %reject logon
            User_List;
        false -&gt;
            From ! {messenger, logged_on},
            link(From),
            [{From, Name} | User_List]        %add user to the list
    end.

%%% Server deletes a user from the user list
server_logoff(From, User_List) -&gt;
    lists:keydelete(From, 1, User_List).


%%% Server transfers a message between user
server_transfer(From, To, Message, User_List) -&gt;
    %% check that the user is logged on and who he is
    case lists:keysearch(From, 1, User_List) of
        false -&gt;
            From ! {messenger, stop, you_are_not_logged_on};
        {value, {_, Name}} -&gt;
            server_transfer(From, Name, To, Message, User_List)
    end.

%%% If the user exists, send the message
server_transfer(From, Name, To, Message, User_List) -&gt;
    %% Find the receiver and send the message
    case lists:keysearch(To, 2, User_List) of
        false -&gt;
            From ! {messenger, receiver_not_found};
        {value, {ToPid, To}} -&gt;
            ToPid ! {message_from, Name, Message}, 
            From ! {messenger, sent} 
    end.

%%% User Commands
logon(Name) -&gt;
    case whereis(mess_client) of 
        undefined -&gt;
            register(mess_client, 
                     spawn(messenger, client, [server_node(), Name]));
        _ -&gt; already_logged_on
    end.

logoff() -&gt;
    mess_client ! logoff.

message(ToName, Message) -&gt;
    case whereis(mess_client) of % Test if the client is running
        undefined -&gt;
            not_logged_on;
        _ -&gt; mess_client ! {message_to, ToName, Message},
             ok
end.

%%% The client process which runs on each user node
client(Server_Node, Name) -&gt;
    {messenger, Server_Node} ! {self(), logon, Name},
    await_result(),
    client(Server_Node).

client(Server_Node) -&gt;
    receive
        logoff -&gt;
            exit(normal);
        {message_to, ToName, Message} -&gt;
            {messenger, Server_Node} ! {self(), message_to, ToName, Message},
            await_result();
        {message_from, FromName, Message} -&gt;
            io:format("Message from ~p: ~p~n", [FromName, Message])
    end,
    client(Server_Node).

%%% wait for a response from the server
await_result() -&gt;
    receive
        {messenger, stop, Why} -&gt; % Stop the client 
            io:format("~p~n", [Why]),
            exit(normal);
        {messenger, What} -&gt;  % Normal response
            io:format("~p~n", [What])
    after 5000 -&gt;
            io:format("No response from server~n", []),
            exit(timeout)
    end.</code></pre><p>The following changes are added:</p><p>The messenger server traps exits. If it receives an exit signal,
<strong>{'EXIT',From,Reason}</strong>, this means that a client process has
terminated or is unreachable for one of the following reasons:</p><ul><li>The user has logged off (the "logoff" message is removed).</li><li>The network connection to the client is broken.</li><li>The node on which the client process resides has gone down.</li><li>The client processes has done some illegal operation.</li></ul><p>If an exit signal is received as above, the tuple
<strong>{From,Name}</strong> is deleted from the servers <strong>User_List</strong> using
the <strong>server_logoff</strong> function. If the node on which the server
runs goes down, an exit signal (automatically generated by
the system) is sent to all of the client processes:
<strong>{'EXIT',MessengerPID,noconnection}</strong> causing all the client
processes to terminate.</p><p>Also, a time-out of five seconds has been introduced in
the <strong>await_result</strong> function. That is, if the server does not
reply within five seconds (5000 ms), the client terminates. This
is only needed in the logon sequence before the client and the
server are linked.</p><p>An interesting case is if the client terminates before
the server links to it. This is taken care of because linking to a
non-existent process causes an exit signal,
<strong>{'EXIT',From,noproc}</strong>, to be automatically generated. This is
as if the process terminated immediately after the link operation.</p></body></html>