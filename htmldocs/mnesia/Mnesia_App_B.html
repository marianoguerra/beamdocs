<!doctype html>
<html><head><meta charset="utf-8"><link rel="stylesheet" href="https://stackpath.bootstrapcdn.com/bootstrap/4.4.0/css/bootstrap.min.css"></head><body style="margin: 4em 10%"><h1>Appendix B: Activity Access Callback Interface</h1><h2>mnesia_access Callback Behavior</h2><pre><code class="">


-module(mnesia_frag).

%% Callback functions when accessed within an activity
-export([
	 lock/4,
	 write/5, delete/5, delete_object/5,
	 read/5, match_object/5, all_keys/4,
	 select/5,select/6,select_cont/3,
	 index_match_object/6, index_read/6,
	 foldl/6, foldr/6, table_info/4,
	 first/3, next/4, prev/4, last/3,
	 clear_table/4
       ]).
</code></pre><pre><code class="">


%% Callback functions which provides transparent
%% access of fragmented tables from any activity
%% access context.

lock(ActivityId, Opaque, {table , Tab}, LockKind) -&gt;
    case frag_names(Tab) of
	[Tab] -&gt;
	    mnesia:lock(ActivityId, Opaque, {table, Tab}, LockKind);
	Frags -&gt;
	    DeepNs = [mnesia:lock(ActivityId, Opaque, {table, F}, LockKind) ||
			 F &lt;- Frags],
	    mnesia_lib:uniq(lists:append(DeepNs))
    end;

lock(ActivityId, Opaque, LockItem, LockKind) -&gt;
    mnesia:lock(ActivityId, Opaque, LockItem, LockKind).

write(ActivityId, Opaque, Tab, Rec, LockKind) -&gt;
    Frag = record_to_frag_name(Tab, Rec),
    mnesia:write(ActivityId, Opaque, Frag, Rec, LockKind).

delete(ActivityId, Opaque, Tab, Key, LockKind) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    mnesia:delete(ActivityId, Opaque, Frag, Key, LockKind).

delete_object(ActivityId, Opaque, Tab, Rec, LockKind) -&gt;
    Frag = record_to_frag_name(Tab, Rec),
    mnesia:delete_object(ActivityId, Opaque, Frag, Rec, LockKind).

read(ActivityId, Opaque, Tab, Key, LockKind) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    mnesia:read(ActivityId, Opaque, Frag, Key, LockKind).

match_object(ActivityId, Opaque, Tab, HeadPat, LockKind) -&gt;
    MatchSpec = [{HeadPat, [], ['$_']}],
    select(ActivityId, Opaque, Tab, MatchSpec, LockKind).

select(ActivityId, Opaque, Tab, MatchSpec, LockKind) -&gt;
    do_select(ActivityId, Opaque, Tab, MatchSpec, LockKind).


select(ActivityId, Opaque, Tab, MatchSpec, Limit, LockKind) -&gt;
    init_select(ActivityId, Opaque, Tab, MatchSpec, Limit, LockKind).


all_keys(ActivityId, Opaque, Tab, LockKind) -&gt;
    Match = [mnesia:all_keys(ActivityId, Opaque, Frag, LockKind)
	     || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

clear_table(ActivityId, Opaque, Tab, Obj) -&gt;
    [mnesia:clear_table(ActivityId, Opaque, Frag, Obj)  || Frag &lt;- frag_names(Tab)],
    ok.

index_match_object(ActivityId, Opaque, Tab, Pat, Attr, LockKind) -&gt;
    Match =
	[mnesia:index_match_object(ActivityId, Opaque, Frag, Pat, Attr, LockKind)
	 || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

index_read(ActivityId, Opaque, Tab, Key, Attr, LockKind) -&gt;
    Match =
	[mnesia:index_read(ActivityId, Opaque, Frag, Key, Attr, LockKind)
	     || Frag &lt;- frag_names(Tab)],
    lists:append(Match).

foldl(ActivityId, Opaque, Fun, Acc, Tab, LockKind) -&gt;
    Fun2 = fun(Frag, A) -&gt;
		   mnesia:foldl(ActivityId, Opaque, Fun, A, Frag, LockKind)
	   end,
    lists:foldl(Fun2, Acc, frag_names(Tab)).

foldr(ActivityId, Opaque, Fun, Acc, Tab, LockKind) -&gt;
    Fun2 = fun(Frag, A) -&gt;
		   mnesia:foldr(ActivityId, Opaque, Fun, A, Frag, LockKind)
	   end,
    lists:foldr(Fun2, Acc, frag_names(Tab)).

table_info(ActivityId, Opaque, {Tab, Key}, Item) -&gt;
    Frag = key_to_frag_name(Tab, Key),
    table_info2(ActivityId, Opaque, Tab, Frag, Item);
table_info(ActivityId, Opaque, Tab, Item) -&gt;
    table_info2(ActivityId, Opaque, Tab, Tab, Item).

table_info2(ActivityId, Opaque, Tab, Frag, Item) -&gt;
    case Item of
	size -&gt;
	    SumFun = fun({_, Size}, Acc) -&gt; Acc + Size end,
	    lists:foldl(SumFun, 0, frag_size(ActivityId, Opaque, Tab));
	memory -&gt;
	    SumFun = fun({_, Size}, Acc) -&gt; Acc + Size end,
	    lists:foldl(SumFun, 0, frag_memory(ActivityId, Opaque, Tab));
	base_table -&gt;
	    lookup_prop(Tab, base_table);
	node_pool -&gt;
	    lookup_prop(Tab, node_pool);
	n_fragments -&gt;
	    FH = lookup_frag_hash(Tab),
	    FH#frag_state.n_fragments;
	foreign_key -&gt;
	    FH = lookup_frag_hash(Tab),
	    FH#frag_state.foreign_key;
	foreigners -&gt;
	    lookup_foreigners(Tab);
	n_ram_copies -&gt;
	    length(val({Tab, ram_copies}));
	n_disc_copies -&gt;
	    length(val({Tab, disc_copies}));
	n_disc_only_copies -&gt;
	    length(val({Tab, disc_only_copies}));
	n_external_copies -&gt;
	    length(val({Tab, external_copies}));

	frag_names -&gt;
	    frag_names(Tab);
	frag_dist -&gt;
	    frag_dist(Tab);
	frag_size -&gt;
	    frag_size(ActivityId, Opaque, Tab);
	frag_memory -&gt;
	    frag_memory(ActivityId, Opaque, Tab);
	_ -&gt;
	    mnesia:table_info(ActivityId, Opaque, Frag, Item)
    end.

first(ActivityId, Opaque, Tab) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:first(ActivityId, Opaque, Tab);
	FH -&gt;
	    FirstFrag = Tab,
	    case mnesia:first(ActivityId, Opaque, FirstFrag) of
		'$end_of_table' -&gt;
		    search_first(ActivityId, Opaque, Tab, 1, FH);
		Next -&gt;
		    Next
	    end
    end.

search_first(ActivityId, Opaque, Tab, N, FH) when N &lt; FH#frag_state.n_fragments -&gt;
    NextN = N + 1,
    NextFrag = n_to_frag_name(Tab, NextN),
    case mnesia:first(ActivityId, Opaque, NextFrag) of
	'$end_of_table' -&gt;
	    search_first(ActivityId, Opaque, Tab, NextN, FH);
	Next -&gt;
	    Next
    end;
search_first(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.

last(ActivityId, Opaque, Tab) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:last(ActivityId, Opaque, Tab);
	FH -&gt;
	    LastN = FH#frag_state.n_fragments,
	    search_last(ActivityId, Opaque, Tab, LastN, FH)
    end.

search_last(ActivityId, Opaque, Tab, N, FH) when N &gt;= 1 -&gt;
    Frag = n_to_frag_name(Tab, N),
    case mnesia:last(ActivityId, Opaque, Frag) of
	'$end_of_table' -&gt;
	    PrevN = N - 1,
	    search_last(ActivityId, Opaque, Tab, PrevN, FH);
	Prev -&gt;
	    Prev
    end;
search_last(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.

prev(ActivityId, Opaque, Tab, Key) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:prev(ActivityId, Opaque, Tab, Key);
	FH -&gt;
	    N = key_to_n(FH, Key),
	    Frag = n_to_frag_name(Tab, N),
	    case mnesia:prev(ActivityId, Opaque, Frag, Key) of
		'$end_of_table' -&gt;
		    search_prev(ActivityId, Opaque, Tab, N);
		Prev -&gt;
		    Prev
	    end
    end.

search_prev(ActivityId, Opaque, Tab, N) when N &gt; 1 -&gt;
    PrevN = N - 1,
    PrevFrag = n_to_frag_name(Tab, PrevN),
    case mnesia:last(ActivityId, Opaque, PrevFrag) of
	'$end_of_table' -&gt;
	    search_prev(ActivityId, Opaque, Tab, PrevN);
	Prev -&gt;
	    Prev
    end;
search_prev(_ActivityId, _Opaque, _Tab, _N) -&gt;
    '$end_of_table'.

next(ActivityId, Opaque, Tab, Key) -&gt;
    case ?catch_val({Tab, frag_hash}) of
	{'EXIT', _} -&gt;
	    mnesia:next(ActivityId, Opaque, Tab, Key);
	FH -&gt;
	    N = key_to_n(FH, Key),
	    Frag = n_to_frag_name(Tab, N),
	    case mnesia:next(ActivityId, Opaque, Frag, Key) of
		'$end_of_table' -&gt;
		    search_next(ActivityId, Opaque, Tab, N, FH);
		Prev -&gt;
		    Prev
	    end
    end.

search_next(ActivityId, Opaque, Tab, N, FH) when N &lt; FH#frag_state.n_fragments -&gt;
    NextN = N + 1,
    NextFrag = n_to_frag_name(Tab, NextN),
    case mnesia:first(ActivityId, Opaque, NextFrag) of
	'$end_of_table' -&gt;
	    search_next(ActivityId, Opaque, Tab, NextN, FH);
	Next -&gt;
	    Next
    end;
search_next(_ActivityId, _Opaque, _Tab, _N, _FH) -&gt;
    '$end_of_table'.
</code></pre></body></html>